//===== rAthena Script ======================================= 
//= Illusion Enchanter 'Episode 17.1'  (kRO Servers)
//===== By: ================================================== 
//= NotKappa#2400 - https://rathena.org/board/profile/18391-notkappa/
//===== Current Version: ===================================== 
//= 1.0 (Initial release)
//===== Additional Comments: ================================= 
// -
//============================================================

prontera,90,115,5	script	イリュージョニスト#prontera	677,{
	disable_command;
	disable_items;
	.@npc$ = "[ ^0000FFイリュージョニスト^000000 ]";
	if(!metIlluEnchant){
		mes .@npc$,
			"Oh, did you bring Illusion equipment?";
		next;
		select("I don't know what those are...");
		mes .@npc$,
			"I was in a hurry, I should have explained that first.";
		next;
		mes .@npc$,
			"I am an alchemist studying a substance called the Illusion Stone. It's been circulating among adventurers lately.";
		next;
		select("Is this an Illusion?");
		mes .@npc$,
			"We don't know yet, but it's quite interesting.";
		next;
		select("That's amazing!");
		mes .@npc$,
			"I'm guessing you've heard about strange spaces that have opened all over the world. Illusion stones are rare occurrences that appear inside these spaces.";
		next;
		mes .@npc$,
			"Their atomic structure is not consistent with any minerals that I've came across so far...";
		next;
		select("Tell me more!");
		mes .@npc$,
			"To cut the chase, if you bring me Illusion stones, I can upgrade certain pieces of equipment for you.";
		next;
		select("What's the catch?");
		mes .@npc$,
			"There is no danger to the enchanting process. It cannot break your items unless you decide to reset the enchantments.";
		next;
		mes .@npc$,
			"I'll continue to study these Illusion dungeons to be able to create even more powerful equipments!";
		next;
		mes .@npc$,
			"Though, maybe I should have mentioned this earlier... This isn't a free service, I'll need Zeny and Illusion Stones.";
		next;
		mes .@npc$,
			"Did you get all that?";
		next;
		select("Yeah!");
		metIlluEnchant = 1;
		mes .@npc$,
			"Alright then! Let's get started.";
		close;
	}
	
	mes .@npc$,
		"イリュージョンエンチャントを行いますか？";
	next;
	switch(select("What do I need to enchant?", "Enchant Illusion equipment", "Reset Illusion equipment")){
		case 1: // What do I need to enchant?
			mes .@npc$,
				"Remember, I only enchant equipment that you have obtained from exchanging Illusion Stones.";
			next;
			mes .@npc$,
				"You can enchant each item twice, paying 5 Illusion Stones per enchant.";
			next;
			mes .@npc$,
				"There is no chance of failure when enchanting, but there is a chance of failure when you reset the item. This is just how the process works.";
			next;
			mes .@npc$,
				"It costs Zeny to reset the enchants, but the chance of success depends on the amount of Zeny you give me. Give it a shot.";
			close;
		
		case 2: // Enchant Illusion equipment
			
			for(.@i=0;.@i<10;.@i++){
				if (!getequipisequiped(.@i))
					continue;
				.@eqId = getequipid(.@i);
				if (inarray(.MeleeWeaponIds,.@eqId) == -1 && 
					inarray(.RangeWeaponIds,.@eqId) == -1 && 
					inarray(.MagicWeaponIds,.@eqId) == -1 && 
					inarray(.AccessoryIds,.@eqId) == -1 && 
					inarray(.ArmorIds,.@eqId) == -1)
					continue;
				.@eqSlots[getarraysize(.@eqSlots)] = .@i;
				.@cItems[getarraysize(.@cItems)] = .@eqId;
				.@menu$ += getitemname(.@eqId) + ":";
				.@count++;
			}
			if(.@count == 0){
				mes .@npc$,
					"イリュージョン装備をしていません。";
				close;			
			}
			.@s = select(.@menu$) - 1;
			.@slot = .@eqSlots[.@s];
			.@equip_id = .@cItems[.@s];
			.@equip_name$ = getitemname(.@equip_id);
			setarray .@card[0],
				getequipcardid(.@slot,0),
				getequipcardid(.@slot,1),
				getequipcardid(.@slot,2),
				getequipcardid(.@slot,3);
			copyarray .@equip_card[0], .@card[0], 4;	// for final check
			.@equip_refine = getequiprefinerycnt(.@slot);
			
			if (.@card[3] > 0) {
				mes .@npc$;
				mes .@equip_name$ + "は既に限界までエンチャントされています。";
				close;
			}
			if(.@card[2] == 0)
				.@cardSlot = 2;
			else if(.@card[3] == 0)
				.@cardSlot = 3;
				
			if (.@card[.@cardSlot] == 0) {
				.@cost = .costStones;
				if (inarray(.MeleeWeaponIds,.@equip_id) != -1)
					copyarray .@enlist,.MeleeWeaponEn,(.MeleeWeaponEn);
				else if (inarray(.RangeWeaponIds,.@equip_id) != -1)
					copyarray .@enlist,.RangeWeaponEn,(.RangeWeaponEn);
				else if (inarray(.MagicWeaponIds,.@equip_id) != -1)
					copyarray .@enlist,.MagicWeaponEn,(.MagicWeaponEn);
				else if (inarray(.AccessoryIds,.@equip_id) != -1)
					copyarray .@enlist,.AccessoryEnchants,(.AccessoryEnchants);
				else if (inarray(.ArmorIds,.@equip_id) != -1)
					copyarray .@enlist,.ArmorEnchants,(.ArmorEnchants);
				.@enlistsize = getarraysize(.@enlist);
				mes .@npc$;
				mes "^0000FF" + .@equip_name$ + "^000000にエンチャントしたいのですね？";
				mes "エンチャントには、 幻想の石^0000FF" + .@cost + "^000000個が必要です.";
				next;
				mes .@npc$;
				mes "エンチャントでは以下の効果がランダムで付与されます。";
				for( .@i = 0; .@i < .@enlistsize; .@i++ )
					mes "[<ITEM>"+getitemname(.@enlist[.@i])+"<INFO>"+.@enlist[.@i]+"</INFO></ITEM>]";
				next;
				.@s2 = select( "やめる", "エンチャントする") - 2;
				if (.@s2 == -1) {
					mes .@npc$;
					mes "分かりました。次の機会をお待ちしております。";
					close;
				}
				if (inarray(.MeleeWeaponIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .MeleeWeaponEn[rand(0,getarraysize(.MeleeWeaponEn)-1)];
				else if (inarray(.RangeWeaponIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .RangeWeaponEn[rand(0,getarraysize(.RangeWeaponEn)-1)];
				else if (inarray(.MagicWeaponIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .MagicWeaponEn[rand(0,getarraysize(.MagicWeaponEn)-1)];
				else if (inarray(.AccessoryIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .AccessoryEnchants[rand(0,getarraysize(.AccessoryEnchants)-1)];
				else if (inarray(.ArmorIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .ArmorEnchants[rand(0,getarraysize(.ArmorEnchants)-1)];
				.@string$ = "^630000" + (.@cardSlot - 1) + "回目^000000のエンチャント";
			}
			
			if (.@cost > countitem(25271)){
				mes .@npc$;
				mes "幻想の石が " + (.@cost - countitem(25271)) + "個足りません。もっと幻想の石を手に入れたらエンチャントを行いましょう。";
				close;
			}
			
			specialeffect2 EF_REPAIRWEAPON;
			delitem 25271,.@cost;// 幻想の石
			
			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || 
				callfunc("F_IsEquipRefineHack", .@slot, .@equip_refine))
				close;
				
			delequip .@slot;
			mes .@npc$;
			mes .@string$ + "を実施します。";
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
			close;
		case 3: // Reset Illusion equipment
			for(.@i=0;.@i<10;.@i++){
				if (!getequipisequiped(.@i))
					continue;
				.@eqId = getequipid(.@i);
				if (inarray(.MeleeWeaponIds,.@eqId) == -1 && 
					inarray(.RangeWeaponIds,.@eqId) == -1 && 
					inarray(.MagicWeaponIds,.@eqId) == -1 && 
					inarray(.AccessoryIds,.@eqId) == -1 && 
					inarray(.ArmorIds,.@eqId) == -1)
					continue;
				.@eqSlots[getarraysize(.@eqSlots)] = .@i;
				.@cItems[getarraysize(.@cItems)] = .@eqId;
				.@menu$ += getitemname(.@eqId) + ":";
				.@count++;
			}
			
			if(.@count == 0){
				mes .@npc$,
					"対象となるイリュージョン装備をしていませんね。";
				close;			
			}
			
			.@s = select(.@menu$) - 1;
			.@slot = .@eqSlots[.@s];
			.@equip_id = .@cItems[.@s];
			.@equip_name$ = getitemname(.@equip_id);
			setarray .@card[0],
				getequipcardid(.@slot,0),
				getequipcardid(.@slot,1),
				getequipcardid(.@slot,2),
				getequipcardid(.@slot,3);
			copyarray .@equip_card[0], .@card[0], 4;	// for final check
			.@equip_refine = getequiprefinerycnt(.@slot);
			
			if (.@card[2] == 0) {
				mes .@npc$;
				mes .@equip_name$ + " はまだエンチャントされていませんね。";
				close;
			}
			
			mes .@npc$,
				"全てのスロットのエンチャントを削除したいのですね。";
			next;
			mes .@npc$,
				"リセットの対価に何を差し出しますか?";
			next;
			.@p = select("100,000 zeny", "200,000 zeny", "300,000 zeny", "400,000 zeny", "500,000 zeny", "幻想の石 5個");
			.@chance = 0;
			if(.@p < 6){
				.@amt = .@p * 100000;
				if(.@p < 5){
					mes .@npc$,
						"たったの" + F_InsertComma(.@amt) + "ゼニーですか...";
					.@chance = 40 + (.@p*10);
				}else{
					.@chance = 100;
					mes .@npc$,
						F_InsertComma(.@amt) + "ゼニーも!";
					next;
					mes .@npc$,
						"こんなに沢山出す方は初めてです。";
				}
			}else {
				.@amt = 5;
				.@chance = 100;
				mes .@npc$,
					"幻想の石!!";
				emotion ET_SPARK,getnpcid(0,"イリュージョニスト#prontera");
			}
			next;
			mes .@npc$,
				"成功率は" + .@chance + "%です";
			next;
			if(.@chance != 100){
				mes .@npc$,	
					"アイテムが挿入されているカードごと消失する可能性があります！";
				next;
				mes .@npc$,
					"続けてよろしいですか?";
				next;
				if(select("やめる", "続ける") == 1){
					mes .@npc$,
						"Alright, come back when you're confidant.";
					close;
				}
			}
			
			mes .@npc$,
				"対価は:";
			if(.@p == 6)
				mes "幻想の石" + .@amt + "個ですね。";
			else
				mes F_InsertComma(.@amt) + "ゼニーですね。";
			next;
			
			if(select("やめる.", "続ける") == 1){
				mes .@npc$,
					"Alright, come back when you're confidant.";
				close;
			}
			
			if(.@p == 6){
				if(.@amt > countitem(25271)){
					mes .@npc$,
						"幻想の石を"+.@amt+"個もっていません";
					close;
				}
				delitem 25271,.@amt;
			}else{
				if(.@amt > Zeny){
					mes .@npc$,
						"ゼニーが足りません。",
						F_InsertComma(.@amt) + "ゼニー必要です。";
					close;
				}
				Zeny -= .@amt;
			}
			
			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || 
				callfunc("F_IsEquipRefineHack", .@slot, .@equip_refine))
				close;
				
			.@c = rand(100);
			if(.@chance >= .@c){
				specialeffect2 EF_REPAIRWEAPON;
				delequip .@slot;
				mes .@npc$;
				mes "成功しました!";
				getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],0,0;
				close;
			}
			
			specialeffect2 EF_REFINEFAIL;
			delequip .@slot;
			mes .@npc$;
			mes "ごめんなさい、もう少しがんばれたのですが...";
		close;
	}
	end;
OnInit:
	.costStones = 5;
	setarray .MeleeWeaponIds,	28762,1846,28745,13337,13469,28022,
								16065,16063,28725,32005,28023,26007,
								21050,1326,13338;
								
	setarray .RangeWeaponIds,	18149,28254,28244,18174;
								
	setarray .MagicWeaponIds,	28612,26109,2051,28626,2039;
	setarray .AccessoryIds,		28508,28509;
	setarray .ArmorIds,			15348,22192,15195,22133,20840,
								19210,20838,19209,28922,20847,
								19223,22190,19344;
	
	setarray .AccessoryEnchants,4795,4796,4797,4798,	//HP 100~400
								4928,4870,4800,4871,	//SP 10~75
								4700,4701,4702,4703,	//STR 1~4
								4730,4731,4732,4733,	//AGI 1~4
								4740,4741,4742,4743,	//VIT 1~4
								4710,4711,4712,4713,	//INT 1~4
								4720,4721,4722,4723,	//DEX 1~4
								4750,4751,4752,4753;	//LUK 1~4
	
	setarray .ArmorEnchants,	4861,4862,4863,4864,	//Max HP 1~4%
								4993,29208,4992,29210,	//HP/SP Absorption  1~2%
								4994,4995,4997,4998,	//strength/agility Lv 1~2
								29009,29010,29000,29001,//Vitality/Intellect Lv 1~2
								29003,29004,29006,29007,//Dexterity/Luck Lv 1~2
								4700,4701,4702,4703,	//STR 1~4
								4730,4731,4732,4733,	//AGI 1~4
								4740,4741,4742,4743,	//VIT 1~4
								4710,4711,4712,4713,	//INT 1~4
								4720,4721,4722,4723,	//DEX 1~4
								4750,4751,4752,4753;	//LUK 1~4
								
	setarray .MeleeWeaponEn,	4811,4810,4809,4808,4820,		//Fighting Spirit 1~5
								29081,29082,29083,29084,29085,	//Acute Lv 1~5
								29061,29062,29063,29064,29065,	//Mettle Lv 1~5
								4807,4842,						//ASPD 1 2
								4700,4701,4702,4703,			//STR 1~4
								4720,4721,4722,4723,			//DEX 1~4
								4750,4751,4752,4753;			//LUK 1~4
	
	setarray .RangeWeaponEn,	4832,4833,4834,4835,4836,		// Expert Archer 1~5
								29091,29092,29093,29094,29095,	//Master Archer Lv 1~5
								29071,29072,29073,29074,29075,	//Magic Essence Lv 1~5
								4807,4842,						//ASPD 1 2
								4730,4731,4732,4733,			//AGI 1~4
								4720,4721,4722,4723,			//DEX 1~4
								4750,4751,4752,4753;			//LUK 1~4
								
	setarray .MagicWeaponEn,	4815,4814,4813,4812,4826,	//Spell 1~5
								4805,4850,					// Archbishop Lv 1~2
								4710,4711,4712,4713,		//INT 1~4
								4720,4721,4722,4723,		//DEX 1~4
								4750,4751,4752,4753;		//LUK 1~4
end;
}

ven_in01,114,114,3	duplicate(イリュージョニスト#prontera)	イリュージョニスト#venin	677

