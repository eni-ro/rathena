//===== rAthena Script =======================================
//= Horror Toy Factory
//===== Description: =========================================
//= [Official Conversion]
//= Horror Toy Factory Instance
//===== Changelogs: ==========================================
//= 1.0 First version. Uses official script structure and text
//=     from iRO. [Capuche]
//============================================================

xmas,237,303,3	script	キャサリンゼータ	4_F_SKULL06GIRL,{
	if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
		mes "[キャサリンゼータ]";
		mes "Aaa.. Are you going to go into there?";
		next;
		mes "[キャサリンゼータ]";
		mes "You'd better release your transformation if you'd like to enter. It could be dangerous in some ways.";
		close;
	}

	if (BaseLevel < 140) {
		mes "[キャサリンゼータ]";
		mes "あ！　あの、待って下さい！";
		mes "ここはあなたのように";
		mes "経験が浅い方が挑戦するには";
		mes "危険な場所なのです。";
		next;
		mes "[キャサリンゼータ]";
		mes "中に入りたいのであれば、";
		mes "もう少し強くなってからが";
		mes "良いかと思います……。";
		mes "そ……そうですね、";
		mes "だいたい^ff0000BaseLv140^000000くらいあれば";
		mes "大丈夫かなっと……。";
		close;
	}

	mes "^ff0000Be cautious taming or other ways than hunting monsters are not considered as normal killing counts.^000000";
	next;
	mes "[キャサリンゼータ]";
	switch( isbegin_quest(12330) ) {
	case 0:
		mes "そ……そんなに見ないでください。";
		mes "今はこんな顔ですけど";
		mes "元々は違うんです……。";
		mes "ちゃんと、目とかも";
		mes "あったんですよ……。";
		next;
		mes "^0000ff‐目の前にいる少女は、";
		mes "　今にも泣きだしそうな声で";
		mes "　そういいながら、";
		mes "　本来は瞳があったであろう";
		mes "　空洞があなたを見つめてる‐^000000";
		next;
		menu "どうしてそんな顔になったんですか？",-;
		mes "[キャサリンゼータ]";
		mes "話すと少し長くなりますけど……";
		mes "大丈夫……でしょうか？";
		next;
		menu "話を続けて下さい",-;
		mes "[キャサリンゼータ]";
		mes "ど……何処から話せばいいのかな。";
		mes "街の話から始めた方がいいのかな……。";
		next;
		mes "^0000ff‐一度ため息を吐き出した少女は";
		mes "　昔話を話す詩人のような声で話始めた‐^000000";
		next;
		mes "[キャサリンゼータ]";
		mes "この街にはおもちゃ工場が";
		mes "ありますけど……実はその工場の中に、";
		mes "作業員しか知らない別の工場が";
		mes "あるんです。";
		mes "そして……私もそこに勤める";
		mes "職員の一人でした。";
		next;
		mes "[キャサリンゼータ]";
		mes "はじめは幸せでした。";
		mes "仕事が終われば街にみんな集まって、";
		mes "お菓子と温かい紅茶を飲みながら";
		mes "おしゃべりをしたり。";
		mes "毎日、充実していました。";
		next;
		menu "今の雰囲気はそうでもないようだけど……",-;
		mes "[キャサリンゼータ]";
		mes "はい……。";
		mes "いつまでもおもちゃを包装するだけで、";
		mes "生活を維持することはできません。";
		mes "結局、作業員は少しずつ";
		mes "工場からいなくなってしまい、";
		mes "工場の経営は厳しくなっていきました。";
		next;
		mes "[キャサリンゼータ]";
		mes "結局、最後まで残ったのは";
		mes "人形職人のお爺さんと、";
		mes "彼のもとで、弟子として";
		mes "人形作りを手伝いつつ";
		mes "働いていた、私だけに";
		mes "なりました……。";
		next;
		mes "^0000ff‐彼女の感情の微妙な震えで";
		mes "　白い顎が揺れているのが感じられる‐^000000";
		next;
		menu "続けて話を聞く",-;
		mes "[キャサリンゼータ]";
		mes "工場の閉鎖が決定した日。";
		mes "人形職人は最後に作った人形に";
		mes "服を着せながら涙を流していました。";
		next;
		mes "[キャサリンゼータ]";
		mes "もうこれで人形を作ることは";
		mes "永遠にないと思っていたようです。";
		mes "……いまでも昨日のことみたいに";
		mes "思い出すことができます……。";
		next;
		mes "[キャサリンゼータ]";
		mes "その最後の人形に^0000ffセリーヌ・キミ^000000という";
		mes "名前を付けて、人形職人は";
		mes "工場のすべての作業場を";
		mes "止めました……。";
		next;
		menu "人形に名前を？",-;
		mes "[キャサリンゼータ]";
		mes "人形職人にとって人形は";
		mes "ある意味、子供のようなもの。";
		mes "命を吹き込むつもりで真剣に";
		mes "取り組み、作品を生み出します。";
		mes "その中でも、最高傑作が";
		mes "キミです……。";
		next;
		mes "[キャサリンゼータ]";
		mes "あ、すいません。";
		mes "セリーヌ・キミでは長いので";
		mes "親しみをこめ、^0000ffキミ^000000と";
		mes "みんな、呼んでいるんです。";
		next;
		mes "[キャサリンゼータ]";
		mes "人形職人は昔を懐かしみつつ";
		mes "キミを作りあげていきました。";
		mes "できれば以前のように、";
		mes "この工場に人が戻ってほしい……。";
		mes "人形職人のそんな気持ちが、キミに";
		mes "生命を吹き込んだのかもしれません。";
		next;
		menu "人形が生きていたんですか？",-;
		mes "[キャサリンゼータ]";
		mes "はい。^0000ff彼女^000000は生命がある存在に";
		mes "なりました。";
		mes "ところが……。";
		mes "どうしてなのかは分からないのですが";
		mes "私がキミを見かけた時、彼女は";
		mes "怒りと憎悪に囚われており、";
		next;
		mes "[キャサリンゼータ]";
		mes "とても私の声など聞く耳を";
		mes "もたないという感じで";
		mes "無人のおもちゃ工場をただ一人";
		mes "歩き回っていたのです。";
		next;
		mes "[キャサリンゼータ]";
		mes "そんなキミを見ておかしいなと";
		mes "こっそり後をつけたのですが……";
		mes "その時、人形職人のお爺さんが";
		mes "座っているのを見つけたのです。";
		next;
		mes "[キャサリンゼータ]";
		mes "私はお爺さんに駆け寄り、";
		mes "声をかけましたが反応はなく";
		mes "体もすっかり冷たくなっていて";
		mes "事切れていました……。";
		next;
		mes "[キャサリンゼータ]";
		mes "そして……。";
		mes "驚いて動けない私の視界に";
		mes "キミの姿が映りました……。";
		mes "お爺さんの亡骸の背後に立ち、";
		mes "その表情はなにも語らず、";
		mes "ただじっとそこに居ました。";
		next;
		mes "[キャサリンゼータ]";
		mes "私はその姿を見て……";
		mes "なぜか恐怖を感じてしまい……";
		mes "そこで私は気を失いました。";
		mes "どれくらいそうしていたのか、";
		mes "目が覚めると工場の外にいて、";
		mes "私の顔はこんな風になっていました。";
		next;
		mes "^0000ff‐目の前にいる少女の顔は、";
		mes "　明らかに皮膚などがなく";
		mes "　例えるなら顔だけが白骨化";
		mes "　してしまったように見える‐^000000";
		next;
		menu "その顔は、キミという人形が原因？",-;
		mes "[キャサリンゼータ]";
		mes "それが……私にも分からないのです。";
		mes "キミの可能性もありますし、";
		mes "他に原因があるのかもしれません。";
		mes "ただハッキリしていることは";
		mes "気を失ったときに何かがあった。";
		mes "それだけなのです……。";
		next;
		menu "キミが犯人だと思いますか？",-;
		mes "[キャサリンゼータ]";
		mes "……正直に言えば、分からないのです。";
		mes "キミではない気もしますし、";
		mes "そうなると他に誰が？と考えると";
		mes "思い当たる人物はいません……。";
		mes "かと言って、キミだと決め付けるのは";
		mes "早計な気もするのです。";
		next;
		mes "[キャサリンゼータ]";
		mes "だから、真実を知りたいと";
		mes "思ってはいるのですが……。";
		mes "……。";
		next;
		mes "^0000ff‐彼女はそういいながら";
		mes "　自分の身体を抱きしめた‐^000000";
		next;
		mes "[キャサリンゼータ]";
		mes "怖いんです……。";
		mes "行きたくないんです……。";
		mes "またあの工場に行けば、";
		mes "今以上に恐ろしいことが";
		mes "起きるかもしれない……。";
		next;
		mes "[キャサリンゼータ]";
		mes "でも、真実を知りたい……。";
		mes "この顔も、可能なら戻したい。";
		mes "お爺さんの亡骸も、ここではなく";
		mes "きちんと埋葬してあげたい。";
		mes "でも一人では怖くて……！";
		mes "……。";
		next;
		mes "[キャサリンゼータ]";
		mes "……冒険者さん。";
		mes "無理を承知で、言います……。";
		mes "私と一緒に、この工場に入って";
		mes "キミを探していただけませんか!?";
		next;
		menu "協力すると申し出る",-;
		mes "[キャサリンゼータ]";
		mes "そう……ですよね。";
		mes "無理に決ま……えっ!?";
		mes "一緒に行ってくれるのですか!?";
		mes "ああ……まさかそんな人が";
		mes "現れてくれるなんて！";
		next;
		menu "一緒にあの工場にいきましょう",-;
		mes "[キャサリンゼータ]";
		mes "ただ、先ほどもいいましたが";
		mes "おもちゃ工場は危険な状態です。";
		mes "私は全く戦えないし……。";
		mes "道案内程度しか役にたてそうに";
		mes "ありません……。";
		next;
		mes "^0000ff‐彼女は大きく開いたまま";
		mes "　今は閉じることすら叶わない";
		mes "　虚空のくぼみがある顔を";
		mes "　少し伏せたかと思うと";
		mes "　まっすぐにこちらを見て";
		mes "　こう告げた‐^000000";
		next;
		mes "[キャサリンゼータ]";
		mes "それでも私は知りたいのです。";
		mes "本当のことを……。";
		mes "このままでは何も問題は";
		mes "解決しませんし……。";
		mes "なので、宜しくお願いします。";
		next;
		mes "[キャサリンゼータ]";
		mes "そうと決まれば……";
		mes "準備をするので、少し待って下さい。";
		mes "冒険者様も準備ができたら";
		mes "私に声をかけてください。";
		setquest 12330;// Skull-faced Girl
		close;
	case 1:
		mes "[キャサリンゼータ]";
		mes "お待たせしました。";
		mes "準備ができました。";
		mes "冒険者様もよろしいでしょうか？";
		mes "今からあの転送装置に";
		mes "冒険者様の情報を登録し";
		mes "入場申請を行います。";
		completequest 12330;
		break;
	case 2:
		mes "残念ですが、キミの魂はいまだに";
		mes "苦しんでいるようです。";
		mes "お爺さんもこのままじゃ";
		mes "安心できないだろうし……。";
		mes "お暇なときにまた、お願いします。";
		break;
	}
	next;
	if (getcharid(1) < 1) {
		mes "[キャサリンゼータ]";
		mes "この先は危険です。";
		mes "パーティーを組んでから";
		mes "きていただけますか。";
		close;
	}
	switch( checkquest(12331,PLAYTIME) ) {
	case -1:
		if (is_party_leader() == false) {
			mes "[キャサリンゼータ]";
			mes "ご、ごめんなさい。";
			mes "あなたのパーティーのリーダーと";
			mes "話をさせてもらえますか？";
			mes "一気に声をかけられると";
			mes "混乱してしまうのです……。";
			close;
		}
		if (select( "^0000ffおもちゃ工場の鍵を開ける^000000","^ff0000やめる^000000" ) == 1) {
			mes "[キャサリンゼータ]";
			mes "まもなく転送装置が動くと思います。";
			instance_create("Horror Toy Factory");
		}
		close;
	case 0:
	case 1:
		mes "[キャサリンゼータ]";
		mes "せっかく来ていただきましたが";
		mes "工場がまだ稼働時間では";
		mes "ないようですね……。";
		mes "ここで働いているのは";
		mes "おもちゃばかりなので";
		mes "時間に融通がきかなくて……。";
		next;
		mes "[キャサリンゼータ]";
		mes "なのでごめんなさい。";
		mes "工場が稼働するまで";
		mes "中に入ることはできません。";
		mes "せっかく来て頂きましたが";
		mes "時間をおいてから再度、";
		mes "声をかけてもらえると嬉しいです。";
		close;
	case 2:
		mes "^0000ff‐おもちゃ工場が";
		mes "　稼働する時間になりました‐^000000";
		erasequest 12331;
		close;
	}
}

xmas,233,305,4	script	Factory Dimension Telep	PORTAL,{
	if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
		mes "[Factory Dimension Teleporter]";
		mes "There are some work study programs as monster workers during the Horror Toy Factory tour. Therefore, ^ff0000you are unable to enter when you are transformed.^000000";
		close;
	}
	if (BaseLevel < 140) {
		mes "[転送装置]";
		mes "転送に必要な能力が足りません。";
		mes "BaseLv140以上にしてください。";
		close;
	}
	if (getcharid(1) < 1) {
		mes "[転送装置]";
		mes "この先に行くには";
		mes "パーティーを組む必要が";
		mes "あります。";
		mes "パーティーを結成して下さい。";
		close;
	}
	switch( checkquest(12331,PLAYTIME) ) {
	case -1:
		switch( instance_enter("Horror Toy Factory") ) {
		case 3:
			mes "An unknown error has occurred.";
			close;
		case 2:
			mes "[転送装置]";
			mes "工場稼働時間ではないため";
			mes "移動することができません。";
			mes "　";
			mes "‐入場受付が必要なようだ‐";
			close;
		case 1:// dummy
			mes "[転送装置]";
			mes "パーティーが解体されています。";
			mes "再度パーティーを組み直してから";
			mes "入場受付を行ってください。";
			close;
		case 0:
			mapannounce "xmas", "メモリアルダンジョン[Horror of Toy] に入場しました　：　" +strcharinfo(1)+ " (" +strcharinfo(0)+ ")", bc_map, "0x00ff99";
			setquest 12331;// Trail of Toy Factory
			end;
		}
	case 0:
	case 1:
		mes "[転送装置]";
		mes "転送を行った痕跡が残っています。";
		mes "しばらく時間をおいてください。";
		close;
	case 2:
		mes "[転送装置]";
		mes "^0000ff転送の痕跡が消えました。";
		mes "転送装置の利用が";
		mes "可能になりました。^000000";
		erasequest 12331;
		close;
	}
}

1@xm_d,112,20,6	script	キャサリンゼータ#0	4_F_SKULL06GIRL,{
	if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
		mes "[キャサリンゼータ]";
		mes "You cannot come here if you are under the effect of a monster transformation.";
		next;
		mes "[キャサリンゼータ]";
		mes "Please wait until the transformation is finished. You cannot move to another area until you become normal.";
		close;
	}
	if (is_party_leader() == false) {
		mes "[キャサリンゼータ]";
		mes "ご、ごめんなさい……。";
		mes "あまり沢山の人に一気に";
		mes "話しかけられると私、混乱するんです。";
		mes "だから申し訳ないのですが";
		mes "あなたのパーティーのリーダーとのみ";
		mes "話をさせてください。";
		close;
	}
	mes "[キャサリンゼータ]";
	mes "なんとか工場に潜り込めましたね。";
	mes "ここについて詳しい説明を";
	mes "私からした方がよいですか？";
	npctalk "キャサリンゼータ : なんとか工場に潜り込めましたね。ここについて詳しい説明を私からした方がよいですか？";
	next;
	switch( select( "話を終える","話を聞く","説明は必要ないので進む" ) ) {
	case 1:
		mes "[キャサリンゼータ]";
		mes "心の準備が出来たら声をかけて下さい。";
		close;
	case 2:
		'SKIP_TALK=0;
		donpcevent instance_npcname("キャサリンゼータ#01") + "::OnStart";
		close;
	case 3:
		'SKIP_TALK=1;
		donpcevent instance_npcname("キャサリンゼータ#01") + "::OnStart2";
		close;
	}
}

1@xm_d,112,20,1	script	キャサリンゼータ#01	4_F_SKULL06GIRL,{
	end;
OnStart:
	enablenpc instance_npcname("#bgm01");
	enablenpc instance_npcname("キャサリンゼータ#01");
	disablenpc instance_npcname("キャサリンゼータ#0");
	sleep 3000;
	npctalk "キャサリンゼータ : ここは第1工場ですね。主にプレゼントとして渡す前のおもちゃや人形たちを集めておいてあった場所です。";
	sleep 5000;
	npctalk "キャサリンゼータ : ああ、色々と思い出してきました。作業服を着ないでうろうろしていたら、警備兵のおじさんたちが走って来てよく怒られました……。";
	sleep 5000;
	npctalk "キャサリンゼータ : それなのに……あ……。";
	donpcevent instance_npcname("#fac1ct") + "::OnStart";
	sleep 3000;
	mapannounce 'xm_d_map$, "工場案内放送 : おもちゃ工場の家族のみなさ〜ん。起きて下さ〜い。楽しい労働時間がやって来ましたよ〜。", bc_map, "0x00ff44";
	sleep 6000;
	npctalk "キャサリンゼータ : こ……これはいったい？　人の代わりにおもちゃや人形たちが歩き回っています。まるで作業員のように……。";
	sleep 5000;
	npctalk "キャサリンゼータ : どうやら長い間、誰もいなかった工場を誰かが、稼動させているようですね……。";
	sleep 3000;
	mapannounce 'xm_d_map$, "工場案内放送 : 工場内のゴミや有害なものを片付けて、いつものように安全に作業を行って下さい。", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "キャサリンゼータ: It's just like when humans worked here.";
	sleep 3000;
	mapannounce 'xm_d_map$, "工場案内放送 : 今日も一所懸命、子供たちの夢と希望の為にプレゼントを作りましょう。", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "キャサリンゼータ : まるで閉鎖される前に働いていた人たちに似せた……いえ、何もかもが以前と同じようです。すごい……。一体誰が……？";
	sleep 1000;
	mapannounce 'xm_d_map$, "工場案内放送 : 第1ライン作業をはじめて下さい。安全ヘルメットを着用しましょう。", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "キャサリンゼータ : そのためにも、あの人形たちと動くおもちゃボックスを一時的に、働けなくする必要があると思います。";
	sleep 5000;
	npctalk "キャサリンゼータ : え？　働けなくする方法ですか？　……そうですね。行動の制御回路を止めてしまえば大丈夫だと思います。";
	sleep 2000;
	mapannounce 'xm_d_map$, "工場案内放送 : 工場内では作業員証明書や作業服は必ず着用しましょう。そうしないと警備の方たちに怒られますよ〜。", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "キャサリンゼータ : あ、警備兵の役もおもちゃたちがやっているのかな……。面倒なことになりそうですから作業服に着替えたほうがよいかもしれませんね。以前はここの何処かに作業服に着替えるボックスがあったのに……。";
	enablenpc instance_npcname("職員用の衣服ボックス#1");
	enablenpc instance_npcname("職員用の衣服ボックス#2");
	enablenpc instance_npcname("職員用の衣服ボックス#3");
	sleep 5000;
	npctalk "キャサリンゼータ : 私の後ろにありましたね。あの職員用の衣服ボックスがそうです。何かあった時の為に、作業員服に必ず着替えて下さい。私はまだ自分の作業員証明証をもっていますから着替える必要はないので……。";
	sleep 5000;
	npctalk "キャサリンゼータ : 第1工場の仕事が終わらないと、第2工場が動きません。とりあえず作業服に着替えたら、活動している人形たちと動くおもちゃボックスの活動を止めてください。";
	sleep 6000;
	disablenpc instance_npcname("キャサリンゼータ#01");
	disablenpc instance_npcname("#bgm01");
	end;

OnStart2:
	enablenpc instance_npcname("#bgm01");
	enablenpc instance_npcname("キャサリンゼータ#01");
	disablenpc instance_npcname("キャサリンゼータ#0");
	sleep 3000;
	npctalk "キャサリンゼータ : では、第2工場への道が開いたら、向こう側で合流することにしましょう。頑張って下さい。";
	sleep 6000;
	disablenpc instance_npcname("キャサリンゼータ#01");
	enablenpc instance_npcname("職員用の衣服ボックス#1");
	enablenpc instance_npcname("職員用の衣服ボックス#2");
	enablenpc instance_npcname("職員用の衣服ボックス#3");
	mapannounce 'xm_d_map$, "工場案内放送 : おもちゃ工場の家族のみなさ〜ん。みんな起きて下さい。楽しい労働時間がやって来ましたよ〜。", bc_map, "0x00ff44";
	sleep 6000;
	mapannounce 'xm_d_map$, "工場案内放送 : 工場内のゴミや有害なものを片付けて、いつものように安全に作業を行って下さい。", bc_map, "0x00ff44";
	sleep 6000;
	mapannounce 'xm_d_map$, "工場案内放送 : 今日も一所懸命、子供たちの夢と希望の為にプレゼントを作りましょう。", bc_map, "0x00ff44";
	donpcevent instance_npcname("#fac1ct") + "::OnStart";
	disablenpc instance_npcname("#bgm01");
	end;
}

1@xm_d,112,20,0	script	#bgm01	-1,9,9,{
	end;
OnTouch:
	playBGM "99";
	end;
}

1@xm_d,13,105,6	script	職員用の衣服ボックス#1	4_NONMYSTCASE,{
	progressbar "ffff00",1;
	playBGM "52";
	.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
	if (.@mob_id_transform == 1246 || .@mob_id_transform < 1) {
		mes "^0000ffYou put on a factory uniform. There still seems to be a few uniforms left in the box.^000000";
		transform 1246,180000;// COOKIE_XMAS
		close;
	}
	mes "^0000ff‐作業服に着替え終わりました。";
	mes "　作業中に変身が必要な場合はここで";
	mes "　いつでも変身することが出来ます‐^000000";
	close;
}
1@xm_d,116,16,6	duplicate(職員用の衣服ボックス#1)	職員用の衣服ボックス#2	4_NONMYSTCASE
1@xm_d,10,20,6	duplicate(職員用の衣服ボックス#1)	職員用の衣服ボックス#3	4_NONMYSTCASE

1@xm_d,1,5,3	script	#fac1ct	CLEAR_NPC,{
	end;
OnStart:
	.@event$ = instance_npcname("#fac1ct") + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,16,24,114,112, "Vicious Cookie",2989,31, .@event$;	// XM_COOKIE
	areamonster 'xm_d_map$,16,24,114,112, "Evil Dwelling Box",2991,36, .@event$;// XM_MYSTCASE
	end;

OnMyMobDead:
	if (mobcount( 'xm_d_map$, instance_npcname("#fac1ct") + "::OnMyMobDead" ) < 30)
		initnpctimer;
	end;

OnTimer1000:
	killmonster 'xm_d_map$, instance_npcname("#fac1ct") + "::OnMyMobDead";
	enablenpc instance_npcname("#fac1bs");
	mapannounce 'xm_d_map$, "作業班長案内放送 : あれ？　みんなどこに行ってしまったんだ!?", bc_map, "0xff8800";
	mapannounce 'xm_d_map$, "作業班長案内放送 : 包装済みのボックスを第2工場へ運ぶのに人手が足りないんだ！", bc_map, "0xff8800";
	mapannounce 'xm_d_map$, "作業班長案内放送 : 手が空いているやつは北西にあるレールのところまで来てくれ！", bc_map, "0xff8800";
	for ( .@i = 61; .@i <= 89; .@i++ )
		disablenpc instance_npcname( "alert#"+ .@i );
	stopnpctimer;
	end;
}

1@xm_d,71,129,3	script	#fac1bs	4_M_COOKIE,{
	if (is_party_leader() == true) {
		.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
		mes "[作業班長]";
		if (.@mob_id_transform == 1246) {
			mes "[作業班長]";
			mes "作業員がいなくなって心配していたぞ。";
			mes "まったく……他の奴らはどこいったんだ？";
			mes "ああっと、しまった！";
			mes "こんなことをしている場合じゃない、";
			mes "子供達がプレゼントを待ってるんだ。";
			next;
			mes "[作業班長]";
			mes "急いで^ff0000ここにある";
			mes "包装済みのボックス^000000";
			mes "を持ってレールの東にある";
			mes "第2工場に移動してくれ。";
			mes "大きいから気をつけて運ぶんだぞ。";
			enablenpc instance_npcname("#pck1");
			npctalk "作業班長 : 急いでここにある包装済みのボックスを持ってレールの東にある第2工場に移動してくれ。大きいから気をつけて運ぶんだぞ。";
		}
		else if (.@mob_id_transform == 1249){
			mes "何をグズグズしているんだ。";
			mes "ここにある包装済みのボックスを";
			mes "持ってからそっちに行くんだ！";
		}
		else {
			mes "なんだ!?　人間じゃないか!!";
			mes "どうして人間が!?";
			mes "ここは関係者以外立ち入り禁止だ！";
			mes "警備兵ー！　人間だー！";
			donpcevent instance_npcname("#fac1bs") + "::OnAlert";
		}
		close;
	}
	end;

OnAlert:
	.@npc_name$ = instance_npcname("#fac1bs");
	.@event$ = .@npc_name$ + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	npctalk "作業班長 : 警備兵！　警備兵はどこにいるんだ！　ここに人間たちが来ている！";
	sleep 3000;
	disablenpc .@npc_name$;
	for ( .@i = 0; .@i <= 10; .@i++ ) {
		areamonster 'xm_d_map$,61,118,71,128, "Guard",2990,1, .@event$;// XM_CRUISER
		sleep 300;
	}
	initnpctimer;
	end;
OnMyMobDead:
	end;

OnTimer60000:
	.@npc_name$ = instance_npcname("#fac1bs");
	killmonster 'xm_d_map$, .@npc_name$ + "::OnMyMobDead";
	enablenpc .@npc_name$;
	npctalk "作業班長 : 人間たちは全部排除されたみたいだな……？　最近こんなことが頻繁に起きている。嫌な世の中になったものだ……。おっと、仕事仕事っと。";
	stopnpctimer;
	end;
}

1@xm_d,65,127,6	script	#pck1	4_NONMYSTCASE,{
	progressbar "ffff00",1;
	.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
	if (.@mob_id_transform == 1246) {
		mes "^0000ff‐包装済みのボックスを";
		mes "　持ち上げました。";
		mes "　あまりにも巨大で";
		mes "　前がちゃんと見えません‐^000000";
		transform 1249,180000;// MYSTCASE
	}
	else if (.@mob_id_transform == 1249){
		mes "^009900‐既に包装済みのボックスを";
		mes "　持ち上げています。";
		mes "　一度に2個以上は持ち上げることは";
		mes "　できません‐^000000";
	}
	else{
		mes "^ff0000‐作業員の姿ではないので、";
		mes "　包装済みのボックスを";
		mes "　持ち上げることができません。";
		mes "　着替えた状態で";
		mes "　作業服に着替えた状態で";
		mes "　試してみましょう‐^000000";
	}
	close;
}

1@xm_d,76,129,0	script	#fac1wp	WARPNPC,2,2,{
	end;
OnTouch:
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 1249) {
		warp 'xm_d_map$,88,129;
		playBGM "54";
	}
	end;
}

1@xm_d,179,129,0	script	#fac2wp	WARPNPC,2,2,{
	end;
OnTouch:
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 1249) {
		warp 'xm_d_map$,183,100;
		playBGM "54";
	}
	end;
}

1@xm_d,1,5,3	script	#alert1	CLEAR_NPC,{
	end;
OnStart:
	.@r = rand(1,10);
	if (.@r == 1)
		mapannounce 'xm_d_map$, "Factory announcement: Outsiders are spotted on the product line. Guards, please move out immediately.",bc_map,"0x00ffff";
	else if (.@r == 2)
		mapannounce 'xm_d_map$, "Factory announcement: Invaders have been identified. Distinction Code AX0829. Type: Human. Chase them away.", bc_map, "0x00ffff";
	else if (.@r == 3)
		mapannounce 'xm_d_map$, "Guard's announcement: Please leave here immediately, outsiders.", bc_map, "0xffff00";
	else if (.@r == 4)
		mapannounce 'xm_d_map$, "Factory announcement: Outsiders, hold up your hands and surrender. Otherwise, I will shoot you.", bc_map, "0x00ffff";
	else if (.@r == 5)
		mapannounce 'xm_d_map$, "Factory announcement: Dispatch the guards. Suppress the invaders.", bc_map, "0x00ffff";
	else if (.@r == 6)
		mapannounce 'xm_d_map$, "CAUTION: The plant manger is coming to inspect. Wipe out the outsiders.", bc_map, "0xff4444";
	else if (.@r == 7)
		mapannounce 'xm_d_map$, "Factory announcement: Not good news. Outside creatures are detected. Guards, please mobilize.", bc_map, "0x00ff88";
	else if (.@r == 8)
		mapannounce 'xm_d_map$, "Factory announcement: Outsiders or invaders are obstacles to factory's operation. You can kill them if necessary.", bc_map, "0xff9999";
	else if (.@r == 9)
		mapannounce 'xm_d_map$, "Factory announcement: Okay that's it, party's over! Get out of my house!", bc_map, "0x00ffff";
	else
		mapannounce 'xm_d_map$, "Guard's announcement: Invaders are spotted! They seem human! I will blip them off!", bc_map, "0xffff00";
	end;
}

1@xm_d,10,24,0	script	alert#61	-1,10,10,{
	end;
OnTouch_:
	if (getstatus(SC_MONSTER_TRANSFORM,1) != 1246) {
		switch( atoi(strnpcinfo(2)) ) {
			case 63: case 66:
			case 69: case 72:
			case 79: case 82:
			case 85: case 88:
				.@count = 4;
				break;
			case 61: case 64:
			case 67: case 70:
			case 73: case 75:
			case 77: case 80:
			case 83: case 86:
			case 89:
				.@count = 6;
				break;
			default:
				.@count = 5;
				break;
		}
		getmapxy .@map$, .@x, .@y;
		.@npc_name$ = instance_npcname( strnpcinfo(0) );
		.@event$ = .@npc_name$ + "::OnMyMobDead";
		playBGM "125";
		specialeffect EF_VENOMDUST;
		donpcevent instance_npcname("#alert1") + "::Onstart";
		disablenpc .@npc_name$;
		killmonster 'xm_d_map$, .@event$;
		areamonster 'xm_d_map$,(.@x-10),(.@y-10),(.@x+10),(.@y+10), "Toy Factory Guard",2990,.@count, .@event$;// XM_CRUISER
		initnpctimer;
	}
	end;

OnTimer45000:
	enablenpc instance_npcname( strnpcinfo(0) );
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) +"::OnMyMobDead";
	stopnpctimer;
	end;

OnMyMobDead:
	end;
}
1@xm_d,30,24,0	duplicate(alert#61)	alert#62	-1,10,10
1@xm_d,50,24,0	duplicate(alert#61)	alert#63	-1,10,10
1@xm_d,70,24,0	duplicate(alert#61)	alert#64	-1,10,10
1@xm_d,90,24,0	duplicate(alert#61)	alert#65	-1,10,10
1@xm_d,10,44,0	duplicate(alert#61)	alert#66	-1,10,10
1@xm_d,30,44,0	duplicate(alert#61)	alert#67	-1,10,10
1@xm_d,50,44,0	duplicate(alert#61)	alert#68	-1,10,10
1@xm_d,70,44,0	duplicate(alert#61)	alert#69	-1,10,10
1@xm_d,90,44,0	duplicate(alert#61)	alert#70	-1,10,10
1@xm_d,110,44,0	duplicate(alert#61)	alert#71	-1,10,10
1@xm_d,10,64,0	duplicate(alert#61)	alert#72	-1,10,10
1@xm_d,30,64,0	duplicate(alert#61)	alert#73	-1,10,10
1@xm_d,50,64,0	duplicate(alert#61)	alert#74	-1,10,10
1@xm_d,70,64,0	duplicate(alert#61)	alert#75	-1,10,10
1@xm_d,90,64,0	duplicate(alert#61)	alert#76	-1,10,10
1@xm_d,110,64,0	duplicate(alert#61)	alert#77	-1,10,10
1@xm_d,10,84,0	duplicate(alert#61)	alert#78	-1,10,10
1@xm_d,30,84,0	duplicate(alert#61)	alert#79	-1,10,10
1@xm_d,50,84,0	duplicate(alert#61)	alert#80	-1,10,10
1@xm_d,70,84,0	duplicate(alert#61)	alert#81	-1,10,10
1@xm_d,90,84,0	duplicate(alert#61)	alert#82	-1,10,10
1@xm_d,110,84,0	duplicate(alert#61)	alert#83	-1,10,10
1@xm_d,10,104,0	duplicate(alert#61)	alert#84	-1,10,10
1@xm_d,30,104,0	duplicate(alert#61)	alert#85	-1,10,10
1@xm_d,50,104,0	duplicate(alert#61)	alert#86	-1,10,10
1@xm_d,70,104,0	duplicate(alert#61)	alert#87	-1,10,10
1@xm_d,90,104,0	duplicate(alert#61)	alert#88	-1,10,10
1@xm_d,110,104,0	duplicate(alert#61)	alert#89	-1,10,10
1@xm_d,155,20,0	duplicate(alert#61)	alert#90	-1,10,10
1@xm_d,180,50,0	duplicate(alert#61)	alert#91	-1,10,10
1@xm_d,205,80,0	duplicate(alert#61)	alert#92	-1,10,10
1@xm_d,230,110,0	duplicate(alert#61)	alert#93	-1,10,10
1@xm_d,180,20,0	duplicate(alert#61)	alert#94	-1,10,10
1@xm_d,180,50,0	duplicate(alert#61)	alert#95	-1,10,10
1@xm_d,180,80,0	duplicate(alert#61)	alert#96	-1,10,10
1@xm_d,205,20,0	duplicate(alert#61)	alert#97	-1,10,10
1@xm_d,205,50,0	duplicate(alert#61)	alert#98	-1,10,10
1@xm_d,205,80,0	duplicate(alert#61)	alert#99	-1,10,10
1@xm_d,205,110,0	duplicate(alert#61)	alert#100	-1,10,10
1@xm_d,230,20,0	duplicate(alert#61)	alert#101	-1,10,10
1@xm_d,230,50,0	duplicate(alert#61)	alert#102	-1,10,10
1@xm_d,230,80,0	duplicate(alert#61)	alert#103	-1,10,10
1@xm_d,230,110,0	duplicate(alert#61)	alert#104	-1,10,10


1@xm_d,185,100,6	script	キャサリンゼータ#2	4_F_SKULL06GIRL,{
	if (is_party_leader() == true) {
		mes "[キャサリンゼータ]";
		mes "無事に合流できましたね、";
		mes "良かった……。";
		mes "怪我とか、ありませんか？";
		mes "休憩をかねて、";
		mes "少し状況を整理しましょうか？";
		npctalk "キャサリンゼータ : 無事に合流できましたね、良かった……。怪我とか、ありませんか？　休憩をかねて、少し状況を整理しましょうか？";
		next;
		switch( select( "話をやめる","第2工場の情報を聞く","説明は不要なので進もうという" ) ) {
		case 1:
			mes "[キャサリンゼータ]";
			mes "準備が出来たら声をかけて下さい。";
			close;
		case 2:
			viewpoint 1,155,98,1,0xFF8000;
			viewpoint 1,130,72,2,0xFF8000;
			viewpoint 1,134,34,3,0xFF8000;
			viewpoint 1,195,28,4,0xFF8000;
			viewpoint 1,228,30,5,0xFF8000;
			viewpoint 1,203,55,6,0xFF8000;
			viewpoint 1,132,52,7,0xFFF8000;
			viewpoint 1,162,52,8,0xFF8000;
			viewpoint 1,242,17,9,0xFF8000;
			viewpoint 1,209,15,10,0xFF8000;
			donpcevent instance_npcname("キャサリンゼータ#21") + "::OnStart";
			close;
		case 3:
			'SKIP_TALK=1;
			viewpoint 1,155,98,1,0xFF8000;
			viewpoint 1,130,72,2,0xFF8000;
			viewpoint 1,134,34,3,0xFF8000;
			viewpoint 1,195,28,4,0xFF8000;
			viewpoint 1,228,30,5,0xFF8000;
			viewpoint 1,203,55,6,0xFF8000;
			viewpoint 1,132,52,7,0xFF8000;
			viewpoint 1,162,52,8,0xFF8000;
			viewpoint 1,242,17,9,0xFF8000;
			viewpoint 1,209,15,10,0xFF8000;
			donpcevent instance_npcname("キャサリンゼータ#21") + "::OnStart2";
			close;
		}
	}
	mes "[キャサリンゼータ]";
	mes "Hold on~ I am talking to your manager, so please wait.";
	close;
}

1@xm_d,185,100,6	script	キャサリンゼータ#21	4_F_SKULL06GIRL,{
	end;
OnStart:
	callsub S_Skip,0;
OnStart2:
	callsub S_Skip,1;
S_Skip:
	enablenpc instance_npcname("#bgm06");
	enablenpc instance_npcname("キャサリンゼータ#21");
	disablenpc instance_npcname("キャサリンゼータ#2");
	sleep 3000;
	if (getarg(0) == 1)
		npctalk "キャサリンゼータ: If you find all clues, search other places. I will look for the spot where I met the doll maker last time I was here.";
	else {
		npctalk "キャサリンゼータ : ここは作業員たちがプレゼントを包装して集める第2工場でした。";
		sleep 5000;
		npctalk "キャサリンゼータ : 以前は工場のあらゆる場所が人々で賑わっていたのに、今はそうでもないようですね……。";
		sleep 5000;
		npctalk "キャサリンゼータ : あ、そうだ！　来る途中に分かったことですけど、ここにはおもちゃではない子供たちも徘徊しています。";
		sleep 5000;
		npctalk "キャサリンゼータ : なんというか……とても怖い姿で。とても悲しそうな感じの霊魂たちが多かったんです。";
		sleep 5000;
		npctalk "キャサリンゼータ : その子たちが襲ってきたら……残念ですが冒険者様の手で休ませてあげて下さい……。それしか、あの子達をここから救う手段はないのです……。なぜか私には襲いかかってきませんでしたが……。";
		sleep 5000;
		npctalk "キャサリンゼータ : あとは、探索している途中に作業中のおもちゃを見かけたら、人形職人の話を聞いてもらってもいいですか？　どうも人形職人の記憶がすっかり消えているようなのです……。";
		sleep 5000;
		npctalk "キャサリンゼータ : もし人形職人への記憶を思い出したら、彼らも旅立つことができるかもしれません。それが唯一の希望のような気がします……。";
		sleep 5000;
		npctalk "キャサリンゼータ : 何かあったら、ご連絡します。それでは、私は何か手がかりがないか、ほかの場所をあたってみますね。";
		sleep 5000;
		npctalk "キャサリンゼータ : あ、あと……包装済みのボックスはもう必要ないはずです。そのままでは怪しまれるので、この後忘れずに作業服に着替えておいてください。";
		sleep 6000;
	}
	disablenpc instance_npcname("キャサリンゼータ#21");
	enablenpc instance_npcname("職員用の衣服ボックス#4");
	donpcevent instance_npcname("#fac2ct") + "::OnStart";

	for ( .@i = 1; .@i <= 10; .@i++ )
		enablenpc instance_npcname( "Worker#"+ .@i );
	disablenpc instance_npcname("#bgm06");
	end;
}

1@xm_d,185,100,0	script	#bgm06	-1,9,9,{
	end;
OnTouch:
	playBGM "99";
	end;
}

1@xm_d,185,94,6	script	職員用の衣服ボックス#4	4_NONMYSTCASE,{
	progressbar "ffff00",1;
	playBGM "128";
	.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
	if (.@mob_id_transform == 1246 || .@mob_id_transform == 1249 || .@mob_id_transform < 1) {
		mes "^0000ff>You changed into uniform.^000000";
		transform 1246,300000;// COOKIE_XMAS
		close;
	}
	mes "^0000ff‐作業服に着替え終わりました。";
	mes "　作業中に変身が必要な場合はここで";
	mes "　いつでも変身することが出来ます‐^000000";
	close;
}

1@xm_d,1,5,3	script	#fac2ct	CLEAR_NPC,{
	end;
OnEnd:
	killmonster 'xm_d_map$, instance_npcname("#fac2ct") + "::OnMyMobDead";
	end;

OnStart:
	.@event$ = instance_npcname("#fac2ct") + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,140,18,240,120, "Malicious Baby Ghost",2993,19, .@event$;	// XM_HYLOZOIST
	areamonster 'xm_d_map$,140,18,240,120, "Evil Dwelling Box",2991,16, .@event$;		// XM_MYSTCASE
	areamonster 'xm_d_map$,140,18,240,120, "Abandoned Teddy bear",2995,22, .@event$;	// XM_TEDDY_BEAR
	areamonster 'xm_d_map$,140,18,240,120, "Creepy Demon",2992,16, .@event$;			// XM_LUDE
	end;
OnMyMobDead:
	end;
}

1@xm_d,1,2,3	script	#fac2wpc	CLEAR_NPC,{
	end;
OnStart:
	.@fac_open = 'worker[1] + 'worker[2] + 'worker[3] + 'worker[4] + 'worker[5] + 'worker[6] + 'worker[7] + 'worker[8] + 'worker[9] + 'worker[10];
	if (.@fac_open == 10) {
		enablenpc instance_npcname("#fac3wp");
		enablenpc instance_npcname("#fac3wp2");
		donpcevent instance_npcname("#fac2ct") + "::OnEnd";
		mapannounce 'xm_d_map$, "工場案内放送 : 配送分類作業場の作業員全員が家に戻りました。作業場の電源を遮断して作業員休憩室の扉を開きます。", bc_map, "0x00ff44";
		for ( .@i = 90; .@i <= 104; .@i++ )
			disablenpc instance_npcname( "alert#"+ .@i );
	}
	else
		mapannounce 'xm_d_map$, "工場案内放送 : 勤務人数を確認中です。今現在包装ラインにて" + (10 - .@fac_open) + "名が作業中です。ミスのないよう、頑張って下さい。", bc_map, "0x00ff44";
	end;
}

1@xm_d,155,98,3	script	Worker#1	4_M_COOKIE,{
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 1246) {
		.@num = atoi(strnpcinfo(2));
		switch(.@num) {
		case 1:
			mes "[作業員]";
			mes "ふむ？　新入りかい？";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "仕事で忙しいんだ。";
				mes "雑談はあとにしてくれ。";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,1,0xFFFFFF;
			npctalk "作業員 : 人形職人……？　あ！　あの爺さんか！　この工場の創業者で人形職人だったな。";
			sleep2 3000;
			npctalk "作業員 : ここにいるおもちゃはみんな、あの爺さんの手で生み出されたといっても過言じゃない。無論、俺もね。";
			sleep2 3000;
			npctalk "作業員 : 中でも、キミちゃんはあの人の最高傑作ともいえるできらしい。爺さんがそう手紙に書いてたのみたよ。";
			sleep2 3000;
			npctalk "作業員 : というかあの爺さんのこと忘れてた……？　あれ……？";
			break;
		case 2:
			mes "[作業員]";
			mes "ふえ？";
			mes "あなた見かけない顔ね。";
			mes "最近、稼働開始したの？";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "仕事は今日もいっぱいいっぱい。";
				mes "わたしは今日も明日も、仕事よ。";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,2,0xFFFFFF;
			npctalk "作業員 : 職人のお爺ちゃんをキミちゃんが殺したって噂があるの？　それについてはまったくの出鱈目よ。";
			sleep2 3000;
			npctalk "作業員 : 職人のお爺ちゃんは、わたし達の生みの親だもの。殺すなんてありえないわ。";
			sleep2 3000;
			npctalk "作業員 : ……キミちゃん、もしかしたらその嘘を真にうけたりしてないかな。あの子、純粋だから……。";
			sleep2 3000;
			npctalk "作業員 : あ、こうしちゃいられない。私も職人のお爺ちゃんに会いに行こう。";
			break;
		case 3:
			mes "[作業員]";
			mes "おい！　いま忙しいんだ。";
			mes "用事があるなら、手短にしてくれ。";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "そっちは仕事ないのか？";
				mes "雑談する暇があるなら働けよ……。";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,3,0xFFFFFF;
			npctalk "作業員 : 人形職人の爺さんは、この工場の創業者でもあり、腕利きの人形職人でねえ。";
			sleep2 3000;
			npctalk "作業員 : 中でも作品のうちの一つである、動く球体関節人形は大人気だった。";
			sleep2 3000;
			npctalk "作業員 : そういやあ、自分も稼働するのやめたはずなのに。ボケたかなあ……。";
			break;
		case 4:
			mes "[作業員]";
			mes "……。";
			mes "あら、何か用なのかな？";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "今日も仕事。明日も仕事。";
				mes "仕事しない悪い人形は怒られるよ？";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,4,0xFFFFFF;
			npctalk "作業員 : キミと人形職人のお爺さんを私が最後に見たのは、休憩室だったかな。";
			sleep2 3000;
			npctalk "作業員 : そういえば最近、休憩室から誰かと誰かの話し声が聞こえるのよね。";
			sleep2 3000;
			npctalk "作業員 : 人形職人のお爺さんはいないのに、誰なんだか。";
			sleep2 3000;
			npctalk "作業員 : ……あれ？　人形職人のお爺さんのこと忘れてた！　いけない！";
			break;
		case 5:
			mes "[作業員]";
			mes "うーん。";
			mes "うーん。";
			mes "……なんだかメモリの調子が悪い。";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "働けど働けど。";
				mes "我の暮らし、楽にならず……。";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,5,0xFFFFFF;
			npctalk "作業員 : うーん、記憶が定かじゃないんだけど……。";
			sleep2 3000;
			npctalk "作業員 : 誰かにお爺さんについて、記憶のメモリーを改竄された可能性があるんだ。";
			sleep2 3000;
			npctalk "作業員 : 人間……なんだろうか？　変な笑い声が印象に残ってるけど、よくおぼえてないや。";
			sleep2 3000;
			npctalk "作業員 : でもおかげで、あのお爺さんを思い出せてよかった。ありがとう。";
			break;
		case 6:
			mes "[作業員]";
			mes "あ〜調子悪い。";
			mes "動力源の不調なのかね。";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "仕事でへとへとでね……。";
				mes "雑談したい気分じゃないんだよ。";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,6,0xFFFFFF;
			npctalk "作業員 : 以前は、観光客が大勢訪れる街だったんだ。爺さんも、嬉しそうだったな。";
			sleep2 3000;
			npctalk "作業員 : ところがある日を境に、詳しい理由はわからないんだけど、観光客も少なくなっていき、この有様だよ。";
			sleep2 3000;
			npctalk "作業員 : さて、爺さんもいないし、このままだと寂しいから、いくとしようかな。";
			break;
		case 7:
			mes "[作業員]";
			mes "やあ。";
			mes "僕になにか用かな。";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "仕事で忙しいんだ。";
				mes "雑談はやめてくれる？";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,7,0xFFFFFF;
			npctalk "作業員 : 爺ちゃんが生きてたころ、たまに赤い服の人間を見かけたなあ。";
			sleep2 3000;
			npctalk "作業員 : 爺ちゃんの知り合いなのか、詳細までは知らないんだけどね。";
			sleep2 3000;
			npctalk "作業員 : 後はよく手紙を書いてたよ。誰に書いてたかは知らないけど。";
			sleep2 3000;
			npctalk "作業員 : 手紙かあ。……僕も爺ちゃんに、手紙を書こうかな。";
			break;
		case 8:
			mes "[作業員]";
			mes "何？";
			mes "私に用ある？";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "私忙しい！　私仕事する!!";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,8,0xFFFFFF;
			npctalk "作業員 : キミ、見なくなった。あの変な人間、キミと話してるのみたの最後。";
			sleep2 3000;
			npctalk "作業員 : おもちゃもいた。でもおもちゃの様子変だった。なにかあった？　おじいさん、なんでいない？";
			sleep2 3000;
			npctalk "作業員 : 私、おじいさん探してくる。いってくる。";
			break;
		case 9:
			mes "[作業員]";
			mes "ららら〜♪";
			mes "！？";
			mes "今の、き、きいてました!?";
			mes "歌いながら仕事すると効率いいの。";
			mes "でも、今のは忘れてください！";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "……あら、なにもないなら";
				mes "仕事に集中させてほしいわ。";
				mes "仕事してればなにも考えないで";
				mes "すむから……。";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,9,0xFFFFFF;
			npctalk "作業員 : お爺様のことですか……？　キミが完成し、目を開けた瞬間、心臓病によって天に召されたようです。";
			sleep2 3000;
			npctalk "作業員 : キミはどうも、自分の顔を見てお爺様は亡くなったものと思っているようですが……。";
			sleep2 3000;
			npctalk "作業員 : ただ、どういう結果であれ、キミは最後にお爺様と会えたのですから。";
			sleep2 3000;
			npctalk "作業員 : え？　今からでも遅くない……ですか。……会えるでしょうか。私も……。";
			break;
		case 10:
			mes "[作業員]";
			mes "ふぁーあ……。";
			mes "むにゃ、なにか用？";
			mes "眠いんだけど……。";
			next;
			if(select("人形職人について尋ねる","何でもない") == 2) {
				mes "[作業員]";
				mes "仕事中の私語は控えろって";
				mes "いわれなかったのー？";
				close;
			}
			close2;
			if('SKIP_TALK==0){
			setpcblock PCBLOCK_NPC, true;
			}
			viewpoint 2,1,1,10,0xFFFFFF;
			npctalk "作業員 : 人間はキミのことを怖がるけれど、キミちゃんは私たちの面倒をよくみてくれるよー。まるで生前のお爺さんみたいに。";
			sleep2 3000;
			npctalk "作業員 : よくキミちゃんに、お爺さんに作ってもらったってアクセサリーのリボンや傘を見せてもらったよー。";
			sleep2 3000;
			npctalk "作業員 : ところでお爺さん、どこにいるのかなー……。";
			break;
		}
		sleep2 3000;
		disablenpc instance_npcname( strnpcinfo(0) );
		if ('worker[.@num] == 0) {
			'worker[.@num] = 1;
			donpcevent instance_npcname("#fac2wpc") + "::OnStart";
		}
		setpcblock PCBLOCK_NPC, false;
		end;
	}
	mes "[作業員]";
	mes "What!? You are not one of us!!";
	donpcevent instance_npcname( strnpcinfo(0) ) + "::OnAlert";
	close;

OnAlert:
	.@npc_name$ = instance_npcname( strnpcinfo(0) );
	killmonster 'xm_d_map$, .@npc_name$ + "::OnMyMobDead";
	.@num = atoi(strnpcinfo(2));
	if (.@num == 8)
		npctalk "作業班長 : 警備兵！　警備兵！";
	else
		npctalk "作業班長 : 警備兵！　警備兵はどこにいるんだ！　ここに人間たちが来ている！";
	sleep 3000;
	switch(.@num) {
	case 9:
		setarray .@coord[0],233,27;
		break;
	case 10:
		setarray .@coord[0],209,27;
		break;
	default:
		getmapxy .@map$, .@coord[0], .@coord[1], BL_NPC;
		break;
	}
	areamonster 'xm_d_map$,(.@coord[0]-8),(.@coord[1]-8),(.@coord[0]+8),(.@coord[1]+8), "Guard",2990,21, .@npc_name$ + "::OnMyMobDead";
	disablenpc .@npc_name$;
	if ('worker[.@num] == 0) {
		'worker[.@num] = 1;
		donpcevent instance_npcname("#fac2wpc") + "::OnStart";
	}
	initnpctimer;
	end;
OnTimer60000:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	stopnpctimer;
	end;

OnMyMobDead:
	end;
}
1@xm_d,130,72,3	duplicate(Worker#1)	Worker#2	4_M_COOKIE
1@xm_d,134,34,1	duplicate(Worker#1)	Worker#3	4_M_COOKIE
1@xm_d,195,28,3	duplicate(Worker#1)	Worker#4	4_M_COOKIE
1@xm_d,228,30,1	duplicate(Worker#1)	Worker#5	4_M_COOKIE
1@xm_d,203,55,3	duplicate(Worker#1)	Worker#6	4_M_COOKIE
1@xm_d,132,52,1	duplicate(Worker#1)	Worker#7	4_M_COOKIE
1@xm_d,162,52,1	duplicate(Worker#1)	Worker#8	4_M_COOKIE
1@xm_d,242,17,5	duplicate(Worker#1)	Worker#9	4_M_COOKIE
1@xm_d,209,15,3	duplicate(Worker#1)	Worker#10	4_M_COOKIE


// Note : aegis script have OnClick part
1@xm_d,131,208,0	script	囚われたサンタ#2	4_M_SANTA,10,10,{
	end;
OnTouch_:
	disablenpc instance_npcname("囚われたサンタ#2");
	enablenpc instance_npcname("囚われたサンタ#3");
	donpcevent instance_npcname("アンソニ#1") + "::OnStart";
	enablenpc instance_npcname("#bgm04");
	end;
}

1@xm_d,131,208,8	script	囚われたサンタ#3	4_M_SANTA,{
	mes "[囚われたサンタ]";
	mes "いくら今はこの工場に";
	mes "主がいないからといって";
	mes "こんな風にこの場所を荒らし";
	mes "好き勝手に物を盗んで";
	mes "許されるわけがない！";
	close;
}

1@xm_d,131,213,4	script	アンソニ#1	4_M_ANTONIO,{
	mes "[アンソニ]";
	mes "おいおい。";
	mes "それじゃあまるで俺様が";
	mes "全部悪いみたいじゃないか。";
	mes "使われなくなった工場をせっかく";
	mes "有効利用してるだけなのに。";
	mes "ククク……。";
	close;

OnStart:
	.@antonio$ = instance_npcname("アンソニ#1");
	.@santa$ = instance_npcname("囚われたサンタ#3");
	
	if('SKIP_TALK==0){
	sleep 3000;
	npctalk "アンソニ : よく聞けよ爺さん。俺はこの工場が気に入っているんだ。", .@antonio$;
	sleep 3000;
	npctalk "アンソニ : この工場の主はもうとっくにくたばっているし、俺様のために働いてくれるようにお願いしてもいいんじゃね〜？。", .@antonio$;
	sleep 3000;
	npctalk "囚われたサンタ : 主にプレゼントの配給を断られた腹いせとはいえ、おかしいと思わないのか？　主がいない状態で、なぜこの工場が動いているのか！。", .@santa$;
	sleep 4000;
	npctalk "アンソニ : よくいうぜ。主がいないってことは誰のものでもないってことだろ？　大体、主がいないのになんで稼動してるのかなんて俺様は知らないし、興味もねーよ。", .@antonio$;
	sleep 4000;
	npctalk "アンソニ : 要は中を覗いたら、この工場が稼動してた。だからそれを俺様が有効に利用しているだけで、爺さんにとやかく言う権利はないはずだぜ？", .@antonio$;
	sleep 4000;
	npctalk "囚われたサンタ : それはお前の都合だろう！　それに子供たちがそんなプレゼントを受け取った時の気持ちを考えてみろ！", .@santa$;
	sleep 4000;
	npctalk "囚われたサンタ : 正規の依頼で作られなかったおもちゃを配られたと知れば、子供たちの気持ちはどうなる!?", .@santa$;
	sleep 4000;
	npctalk "アンソニ : へ……？　子供たちの気持ちだって？", .@antonio$;
	sleep 2000;
	npctalk "アンソニ : 別にいいんじゃない？　結果的にはプレゼントじゃないか。ほれ、タンの盛り合わせもバッタの炒め物も、腹に入れば同じだろ？　ククク……。", .@antonio$;
	sleep 5000;
	npctalk "囚われたサンタ : お前のへりくつは聞いていない！　今すぐにでもこの工場から出て行くんだ！　アンソニ！", .@santa$;
	sleep 5000;
	mapannounce 'xm_d_map$, "工場案内放送 : 第3工場にて発送準備が終わりました。", bc_map, "0x00ff44";
	sleep 5000;
	mapannounce 'xm_d_map$, "工場案内放送 : 配送部の職員たちは準備をして下さい。", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "アンソニ : お？　プレゼントたちが今日もた〜くさん溜まっているな。さて、今日はどんなプレゼントが集まってるかな!?", .@antonio$;
	sleep 4000;
	npctalk "アンソニ : おっと、そこの人間。配送用のおもちゃに手を出さなければ、俺様は人間でも邪険にしないぜ。誰かと違って、心が広いからな。", .@antonio$;
	sleep 4000;
	}
	disablenpc .@antonio$;
	if('SKIP_TALK==0){
	sleep 2000;
	npctalk "囚われたサンタ : ああ……サンタクロースは……子供たちの未来は……どうなってしまうのだ……。", .@santa$;
	sleep 4000;
	npctalk "囚われたサンタ : 君がなぜここにいるのかわからないが……あのアンソニという奴を、実力行使で工場から追い出してくれないだろうか。", .@santa$;
	sleep 4000;
	npctalk "囚われたサンタ : 私たちはこの工場の人形職人からプレゼントの配送を頼まれていたのだが、あのアンソニは人形職人に嫌われ、仕事を干されたんだ。", .@santa$;
	sleep 4000;
	npctalk "囚われたサンタ : それなのにあいつはそれを逆恨みして、なぜか主がいないのに稼動していたこの工場に目を付け、今じゃ我が物顔で闊歩しているんだ。", .@santa$;
	sleep 4000;
	npctalk "囚われたサンタ : しかもあいつを止めにきた私を縛ったあげく、人形職人の娘ともいえる人形も得意の舌先三寸で東の部屋から出てこないように仕向けたようだ。", .@santa$;
	sleep 4000;
	npctalk "囚われたサンタ : 私がなんとかできれば良いのだが、サンタクロース同士は契約の関係上、手を出せないんだ……。", .@santa$;
	sleep 4000;
	}
	npctalk "囚われたサンタ : アンソニは第3工場のどこかにいると思う。すまないが、子供たちやこの工場の未来を頼む……！", .@santa$;

	disablenpc instance_npcname("#bgm04");
	enablenpc instance_npcname("#fac4wp");
	enablenpc instance_npcname("#fac4wp2");
	donpcevent instance_npcname("#fac3ct") + "::OnStart";
	donpcevent instance_npcname("#fac3ct2") + "::OnStart";
	donpcevent instance_npcname("#fac3ct3") + "::OnStart";
	sleep 3000;
	enablenpc instance_npcname("#bgm05");
	end;
}

1@xm_d,131,208,0	script	#bgm04	-1,9,9,{
	end;
OnTouch:
	playBGM "54";
	end;
}

1@xm_d,131,208,0	script	#bgm05	-1,9,9,{
	end;
OnTouch:
	playBGM "105";
	end;
}

1@xm_d,1,5,3	script	#fac3ct	CLEAR_NPC,{
	end;
OnEnd:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,13,144,121,248, "Dancing Marionette",2994,37, .@event$;	// XM_MARIONETTE
	areamonster 'xm_d_map$,13,144,121,248, "Decorated Evil Tree",2987,31, .@event$;	// XM_TREE
	areamonster 'xm_d_map$,13,144,121,248, "Abandoned Teddy bear",2995,43, .@event$;// XM_TEDDY_BEAR
	areamonster 'xm_d_map$,13,144,121,248, "Creepy Demon",2992,31, .@event$;		// XM_LUDE
	end;
OnMyMobDead:
	end;
}

1@xm_d,1,5,3	script	#fac3ct2	CLEAR_NPC,{
	end;
OnEnd:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,159,215,241,247, "Malicious Baby Ghost",2993,13, .@event$;	// XM_HYLOZOIST
	areamonster 'xm_d_map$,159,215,241,247, "Decorated Evil Tree",2987,11, .@event$;	// XM_TREE
	areamonster 'xm_d_map$,159,215,241,247, "Abandoned Teddy bear",2995,15, .@event$;	// XM_TEDDY_BEAR
	areamonster 'xm_d_map$,159,215,241,247, "Creepy Demon",2992,11, .@event$;			// XM_LUDE
	end;
OnMyMobDead:
	end;
}

1@xm_d,1,5,3	script	#fac3ct3	CLEAR_NPC,{
	end;
OnStart:
	if (rand(1,10) > 3)
		areamonster 'xm_d_map$,13,144,121,248 ,"アンソニ",2988,1, instance_npcname("#fac3ct3")+"::OnMyMobDead";// ANTONIO
	else
		areamonster 'xm_d_map$,159,215,241,247, "アンソニ",2988,1, instance_npcname("#fac3ct3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount( 'xm_d_map$, instance_npcname("#fac3ct3") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;

OnTimer1000:
OnEnd:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	donpcevent instance_npcname("#fac3ct") + "::OnEnd";
	donpcevent instance_npcname("#fac3ct2") + "::OnEnd";
	donpcevent instance_npcname("#finalbs") + "::OnStart";
	disablenpc instance_npcname("囚われたサンタ#3");
	mapannounce 'xm_d_map$, "???: I won't harm you if you leave quietly without spoiling it for me.", bc_map, "0xff8800";
	stopnpctimer;
	end;
}

1@xm_d,1,5,3	script	#finalbs	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("キャサリンゼータ#5");
	enablenpc instance_npcname("セリーヌ・キミ#0");
	enablenpc instance_npcname("#fac5wp");
	// enablenpc instance_npcname("#fac5wp2");// never enabled
	enablenpc instance_npcname("#jeton1");
	end;
}

/*
// never enabled
1@xm_d,160,208,0	script	#fac5wp2	WARPNPC,2,2,{
	end;
OnTouch:
	warp 'xm_d_map$,145,208;
	end;
}
*/

1@xm_d,233,183,3	script	セリーヌ・キミ#0	4_F_KIMI,{
	mes "[セリーヌ・キミ]";
	mes "あなたも以前来た人間のように";
	mes "ここを荒らしに来たの……？";
	mes "ねえ、あなたたちはどうして";
	mes "破壊したがるの？";
	close;
}

1@xm_d,222,183,0	script	#jeton1	-1,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#jeton1");
	donpcevent instance_npcname("キャサリンゼータ#5") + "::OnStart";
	enablenpc instance_npcname("#bgm02");
	end;
}

1@xm_d,222,183,0	script	#bgm02	-1,9,9,{
	end;
OnTouch:
	playBGM "101";
	end;
}

1@xm_d,222,183,6	script	キャサリンゼータ#5	4_F_SKULL06GIRL,{
	mes "[キャサリンゼータ]";
	mes "き……気を付けて下さい！";
	mes "キミは普通の状態じゃなさそうです。";
	mes "声をかけているのですが";
	mes "どうも様子がおかしくて……。";
	mes "一緒に声をかけてください！";
	close;

OnStart:
	.@kimi$ = instance_npcname("セリーヌ・キミ#0");
	if('SKIP_TALK==0){
	sleep 2000;
	npctalk "キャサリンゼータ : キミ！　私の話を聞いて。私とこの人は、ここを荒らすために来たわけではない！　あなたと話がしたいの。";
	sleep 3000;
	npctalk "セリーヌ・キミ : 話……？　……こんな恐ろしい外見をしているわたしの話なんて、誰も聞きいてくれない！　おじいちゃんも……おもちゃたちも……！", .@kimi$;
	sleep 4000;
	npctalk "キャサリンゼータ : 私は……あなたがひどいことをする子じゃないって信じてる。　だから教えて、キミ。何があったの？　私のこの顔はあなたのせいなの？";
	sleep 4000;
	mapannounce 'xm_d_map$, "キミの幻影 : 信じる？　……ウソだ！", bc_map, "0xff8800";
	sleep 1000;
	npctalk "セリーヌ・キミ : ひどいことをする子じゃない……？　じゃあ、おじいちゃんはどうして、もう一度目を開けて、わたしをみてくれなかったの!?", .@kimi$;
	sleep 4000;
	npctalk "セリーヌ・キミ : おじいちゃん、わたしの顔を見てびっくりしたあと、わたしから遠ざかって動かなくなった……。わたしがいくら体を揺すっても、名前をよんでも答えてくれなかった……。", .@kimi$;
	sleep 4000;
	mapannounce 'xm_d_map$, "キミの幻影 : ソウダヨ、キミ！　あの人はアナタのことを見て驚いていたヨ！　アナタを恐れていたんダヨ……。カワイソウなキミ……。", bc_map, "0xff8800";
	sleep 3000;
	npctalk "キャサリンゼータ : !?　一体、誰が喋ってるの……。キミ以外に声が……。";
	sleep 3000;
	npctalk "キャサリンゼータ : キミ……！　その声に耳を傾けないで！　お爺さんはあなたの名前を呼ぶ前に、病気のせいで……";
	sleep 3000;
	npctalk "セリーヌ・キミ : おじいちゃん……病気……だったの……？", .@kimi$;
	sleep 3000;
	npctalk "キャサリンゼータ : そう！　病気だよ！　命を吹き込まれたあなたが動いたその瞬間、お爺さんはあまりの喜びに……。";
	sleep 3000;
	npctalk "キャサリンゼータ : キミ……！　その声に耳を傾けないで！　お爺さんはあなたの名前を呼ぶ前に、病気のせいで……";
	sleep 1000;
	mapannounce 'xm_d_map$, "キミの幻影 : ソウ！　驚愕ダよ！　命ヲ吹き込まれたアナタが動いたその瞬間、オ爺さんはあまりの醜さニ……驚いて、心臓がトマッテ死んじゃっタの。", bc_map, "0xff8800";
	sleep 3000;
	npctalk "セリーヌ・キミ : わたし……が……わたしの顔がこんなだから……おじいちゃん、びっくりして心臓がとまっちゃったの……？", .@kimi$;
	sleep 2000;
	npctalk "キャサリンゼータ : キミ、それは違う！　あなたはお爺さんを殺してなんかいない！　お爺さんも、あなたに殺されたなんて思っていない!!";
	sleep 3000;
	npctalk "セリーヌ・キミ : え……？　……わたし、誤解していたの……？　わたしがおじいちゃんを殺したわけじゃないの……？　嫌われたわけじゃないの？", .@kimi$;
	sleep 2000;
	mapannounce 'xm_d_map$, "キミの幻影 : 忘れたノ？　キミ。あの人ガ見せてくれタ自身ノ姿を、再度見てみなさイ。鏡を見テ、ドウ？", bc_map, "0xff8800";
	sleep 3000;
	npctalk "セリーヌ・キミ : わ……わたし……これが……わたし……。醜い……怖い……わたしの醜い心そのまま……！　こんなのじゃ、みんな、みんな……わたしを……", .@kimi$;
	sleep 1000;
	mapannounce 'xm_d_map$, "キミの幻影 : ねエ、キミ。大体お爺さんがキミを愛してたナラ、なんデ、こんな醜い顔にしたの？　お人形は顔が命なのに。クスクス……。", bc_map, "0xff8800";
	sleep 3000;
	npctalk "セリーヌ・キミ : わたしがこの部屋にいる理由……醜いわたしがうろうろしたら、みんな怖がる……逃げるって……言われた……。だからわたし、ここに……。", .@kimi$;
	sleep 3000;
	npctalk "セリーヌ・キミ : おじいちゃんも……わたしなんか愛してなかった……！　赤い服のお姉ちゃんも、わたしを騙して……わたしを笑いものにするつもりだったんでしょ……！", .@kimi$;
	specialeffect EF_MAPPILLAR2, AREA, .@kimi$;
	sleep 3000;
	npctalk "キャサリンゼータ : ち、違う！　……駄目だわ。キミの自我が分裂しています！　このままにしておくと危険です……！　仕方ありません……あの子を、止めてください！";
	sleep 3000;
	npctalk "キャサリンゼータ : 力づくで構いません。キミが壊れたら……私が……私がなんとかしてみせます。なので、冒険者様は自分の安全を優先してください！";
	specialeffect EF_MAPPILLAR2, AREA, .@kimi$;
	sleep 1000;
	mapannounce 'xm_d_map$, "キミの幻影 : 怒りなサイ。悲しみなサイ。ダレもキミの味方なんていないんダヨ。クスクスクスクス……。", bc_map, "0xff8800";
	sleep 3000;
	npctalk "キャサリンゼータ : 工場の空気もおかしい……。冒険者様！　私はキミを止めるための手がかりがないか探してきます！";
	sleep 3000;
	}
	disablenpc instance_npcname("キャサリンゼータ#5");
	sleep 2000;
	disablenpc .@kimi$;
	donpcevent instance_npcname("#finalbs2") + "::OnStart";
	disablenpc instance_npcname("#bgm02");
	end;
}

1@xm_d,1,5,3	script	#finalbs2	CLEAR_NPC,{
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#bgm03");
	.@event$ = instance_npcname("#finalbs2") + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	monster 'xm_d_map$,231,184, "セリーヌ・キミ",2996,1, .@event$;// XM_CELINE_KIMI
	'celene_id = $@mobid[0];
	monster 'xm_d_map$,226,190, "Kimi's Phantom",2997,1, .@event$;// G_XM_CELINE_KIMI
	'phantom_id = $@mobid[0];
	setunitdata 'celene_id, UMOB_HP,20000000;
	setunitdata 'phantom_id,UMOB_HP,20000000;

	unittalk 'celene_id, "I don't want to be deserted. I don't want to be abandoned.";
	initnpctimer;
	end;

OnMyMobDead:
	if (mobcount( 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead" ) < 1)
		donpcevent instance_npcname("#finalbs2") + "::OnEnd";
	end;

OnEnd:
	stopnpctimer;
	killmonster 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead";
	disablenpc instance_npcname("#bgm03");
	donpcevent instance_npcname("#finalbs_e") + "::OnStart";
	end;

OnTalk:
	.@chat_r = rand(1,10);
	if (.@chat_r == 1)
		unittalk 'celene_id, "セリーヌ・キミ : わたしにとっての居場所はここしかないのに……なぜ、放っておいてくれないの……。"; 
	else if (.@chat_r == 2)
		unittalk 'celene_id, "セリーヌ・キミ : 愛なんていらない……必要ない……。わたしには必要ない……。";
	else if (.@chat_r == 3)
		unittalk 'celene_id, "セリーヌ・キミ : わたしは1人でいなきゃいけない……。可愛くないお人形は、誰にも必要とされない……。";
	else if (.@chat_r == 4)
		unittalk 'celene_id, "セリーヌ・キミ : わたしと同じ、醜い顔になろうよ……。……仲間になろうよ。";
	else if (.@chat_r == 5)
		unittalk 'celene_id, "セリーヌ・キミ : 死んじゃえ……。";
	else if (.@chat_r == 6)
		unittalk 'celene_id, "セリーヌ・キミ : やだよ……怖いよ……おじいちゃん……。なんで、ここに居てくれないの……！";
	else
		unittalk 'celene_id, "セリーヌ・キミ : 嫌い。嫌い……！　あっちにいって！";;
	end;

OnTimer1000:
	if (unitexists('celene_id) == true) {
		getunitdata 'celene_id, .@data;
		if ((.@data[UMOB_X] < 211 || .@data[UMOB_X] > 241 || .@data[UMOB_Y] < 166 || .@data[UMOB_Y] > 201) && (.@data[UMOB_X] > 0 || .@data[UMOB_Y] > 0)) {
			mapannounce 'xm_d_map$, "セリーヌ・キミ : ダメ！　この場所から離れちゃダメッ！",bc_map,"0xff6666",FW_NORMAL,15;
			donpcevent instance_npcname("#finalbs2") + "::Onfail";
		}
	}
	end;

Onfail:
	stopnpctimer;
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	enablenpc instance_npcname("セリーヌ・キミ#2");
	disablenpc instance_npcname("#bgm03");
	enablenpc instance_npcname("#kimion1");
	end;

OnTimer5000:
	if (unitexists('celene_id) == true)
		donpcevent instance_npcname("#bssk01") + "::OnStart";
	end;

OnTimer10000:
	if (mobcount( 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead" ) > 1) {
		.@npc_name$ = instance_npcname("#finalbs2");
		getunitdata 'celene_id, .@MOB_HP1;
		getunitdata 'phantom_id, .@MOB_HP2;
		if (.@MOB_HP1[UMOB_HP] > .@MOB_HP2[UMOB_HP]) {
			setarray .@mob_hp[0], .@MOB_HP1[UMOB_HP], .@MOB_HP2[UMOB_HP];
			setarray .@string$[0],
				"セリーヌ・キミ : わたしとあなたは表裏一体。わたしがあなたを回復するの……。",
				"キャサリンゼータ : セリーヌ・キミとキミの幻影の体力が治癒によって";
			.@talk = 'celene_id;
		}
		else if (.@MOB_HP2[UMOB_HP] > .@MOB_HP1[UMOB_HP]) {
			setarray .@mob_hp[0], .@MOB_HP2[UMOB_HP], .@MOB_HP1[UMOB_HP];
			setarray .@string$[0],
				"キミの幻影 : ワタシがキミを回復させル……。",
				"キャサリンゼータ : セリーヌ・キミとキミの幻影の体力が治癒によって";
			.@talk = 'phantom_id;
		}
		.@diff_hp = .@mob_hp[0] - .@mob_hp[1];
		if (.@diff_hp > 100000) {
			.@set_bs_hp = (.@diff_hp * 5) / 10;
			.@MOB_HP3 = .@mob_hp[0] + .@set_bs_hp;
			if (.@MOB_HP3 > 66666666)
				.@MOB_HP3 = 66666666;
			setunitdata 'celene_id, UMOB_HP, .@MOB_HP3;
			setunitdata 'phantom_id, UMOB_HP, .@MOB_HP3;
			donpcevent instance_npcname("#eff_f01") + "::OnStart";
			unittalk .@talk, .@string$[0];
			sleep 1000;
			mapannounce 'xm_d_map$,  .@string$[1] + .@set_bs_hp +" になりました！　気をつけてください！", bc_map, "0xff6666";
			donpcevent instance_npcname("#heal_c") + "::OnStart";
		}
		initnpctimer;
	}
	end;
}

1@xm_d,216,193,3	script	#eff_f01	CLEAR_NPC,{
	end;
OnStart:
	for ( .@i = 1; .@i < 10; .@i++ )
		specialeffect EF_HEARTCASTING, AREA, instance_npcname( "#eff_f0"+ .@i );
	end;
}

1@xm_d,226,193,3	script	#eff_f02	CLEAR_NPC,{
	end;
}
1@xm_d,236,193,3	duplicate(#eff_f02)	#eff_f03	CLEAR_NPC
1@xm_d,216,183,3	duplicate(#eff_f02)	#eff_f04	CLEAR_NPC
1@xm_d,226,183,3	duplicate(#eff_f02)	#eff_f05	CLEAR_NPC
1@xm_d,236,183,3	duplicate(#eff_f02)	#eff_f06	CLEAR_NPC
1@xm_d,216,173,3	duplicate(#eff_f02)	#eff_f07	CLEAR_NPC
1@xm_d,226,173,3	duplicate(#eff_f02)	#eff_f08	CLEAR_NPC
1@xm_d,236,173,3	duplicate(#eff_f02)	#eff_f09	CLEAR_NPC

1@xm_d,1,5,3	script	#bssk01	CLEAR_NPC,{
	end;
OnStart:
	.@r = rand(1,3);
	if (.@r == 1)
		donpcevent instance_npcname("#bssk02") + "::OnStart";
	else if (.@r == 2)
		donpcevent instance_npcname("#bssk03") + "::OnStart";
	end;
}

1@xm_d,1,5,3	script	#bssk02	CLEAR_NPC,{
	end;
OnStart:
	donpcevent instance_npcname("#finalbs2") + "::OnTalk";
	for ( .@i = 1; .@i < 5; .@i++ )
		donpcevent instance_npcname( "#crssk"+ .@i ) + "::OnStart";
	end;
}

1@xm_d,1,5,3	script	#bssk03	CLEAR_NPC,{
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	while (unitexists('celene_id) == true) {
		getunitdata 'celene_id, .@data;
		.@x = .@data[UMOB_X] + rand(1,20) - 10;
		.@y = .@data[UMOB_Y] + rand(1,20) - 10;
		monster 'xm_d_map$,.@x,.@y, "#f_w_1",3038,1, .@event$;// HIDDEN_MOB7
		.@mon_num++;
		if (.@mon_num > 20)
			break;
		sleep 200;
	}
	sleep 6000;
	killmonster 'xm_d_map$, .@event$;
	end;

OnMyMobDead:
	end;
}

1@xm_d,1,5,3	script	#crssk1	CLEAR_NPC,{
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	getunitdata 'celene_id, .@data;
	setarray .@coord[0], .@data[UMOB_X], .@data[UMOB_Y];
	.@num = atoi(strnpcinfo(2));
	.@index = ( .@num > 2 ? 1 : 0 );// x or y
	.@signe = pow(-1,.@num+1);
	while(1) {
		.@coord[.@index] = .@coord[.@index] + (2 * .@signe);
		.@coord[!.@index] = .@coord[!.@index] + rand(0,2) - 1;
		monster 'xm_d_map$,.@coord[0], .@coord[1], "#f_w_1",3038,1, .@event$;
		if (.@coord[0] < 211 || .@coord[0] > 241 || .@coord[1] < 166 || .@coord[1] > 201)
			break;
		sleep 200;
	}
	sleep 6000;
	killmonster 'xm_d_map$, .@event$;
	end;
OnMyMobDead:
	end;
}
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk2	CLEAR_NPC
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk3	CLEAR_NPC
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk4	CLEAR_NPC

1@xm_d,233,183,0	script	#kimion1	-1,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#kimion1");
	donpcevent instance_npcname("セリーヌ・キミ#2") + "::OnStart";
	end;
}

1@xm_d,233,183,3	script	セリーヌ・キミ#2	4_F_KIMI,{
	mes "[セリーヌ・キミ]";
	mes "……わたしは醜い。";
	mes "……わたしは怖い。";
	mes "……みんな本当はわたしを嫌ってる。";
	mes "……おじいちゃんも、おもちゃも。";
	mes "……あなたも、そうでしょ？";
	close;

OnStart:
	npctalk "セリーヌ・キミ :  あなたも……わたしを殺しに来たの？　あなたも……わたしを笑いに来たの？　あなたも……わたしを醜いって思うの？あなたも……わたしを嫌いって言うの？";
	specialeffect EF_MAPPILLAR2;
	sleep 5000;
	disablenpc instance_npcname("セリーヌ・キミ#2");
	donpcevent instance_npcname("#finalbs2") + "::OnStart";
	end;
}

1@xm_d,228,183,0	script	#bgm03	-1,25,25,{
	end;
OnTouch:
	playBGM "123";
	end;
}

1@xm_d,1,5,3	script	#heal_c	CLEAR_NPC,{
	end;
OnStart:
	if (rand(1,10) > 4)
		initnpctimer;
	end;
OnTimer3000:
	mapannounce 'xm_d_map$, "セリーヌ・キミ and her phantom have shared their strength. They will be stronger than ever!",bc_map,"0xff6666";
	stopnpctimer;
	end;
}

1@xm_d,1,5,3	script	#finalbs_e	CLEAR_NPC,{
	end;
OnStart:
	mapannounce 'xm_d_map$, "セリーヌ・キミ : わたしを倒しても無駄だよ……。またわたしはこの工場にもどってくるんだから……。",bc_map,"0xff6666",FW_NORMAL,15;
	enablenpc instance_npcname("#fac6wp");
	enablenpc instance_npcname("#jeton2");
	enablenpc instance_npcname("キャサリンゼータ#6");
	for ( .@i = 1; .@i <= 10; .@i++ )
		enablenpc instance_npcname("Packaged Present#"+ .@i);
	sleep 6000;
	mapannounce 'xm_d_map$, "キャサリンゼータ : !?　キミを退けたんですか!?　凄い……。とりあえず、その部屋の南西にある通路から、私がいる場所へきてください！",bc_map,"0xffff00";
	end;
}

1@xm_d,218,145,0	script	#jeton2	-1,4,4,{
	end;
OnTouch_:
	disablenpc instance_npcname("#jeton2");
	donpcevent instance_npcname("キャサリンゼータ#6") + "::OnStart";
	end;
}

1@xm_d,218,145,5	script	キャサリンゼータ#6	4_F_SKULL06GIRL,{
	end;
OnStart:
	if('SKIP_TALK==0){
	sleep 1000;
	npctalk "キャサリンゼータ : ……ここまでくれば大丈夫なはずです。";
	sleep 3000;
	npctalk "キャサリンゼータ : 結局、手がかりも見つからず、危険な目にあわせてすいません……。";
	sleep 3000;
	npctalk "キャサリンゼータ : でも、最後に少しは私の人形職人見習いとしての目が役にたったみたいで、良かったです。";
	sleep 3000;
	npctalk "キャサリンゼータ : それにしても、幻影の声というものの正体は何だったんですかね？　どうしてキミにあんな酷いことを……。";
	sleep 3000;
	npctalk "キャサリンゼータ : きっと私の顔の呪いもその正体不明の声が犯人ではないかと思っています。確証はないのですが……。";
	sleep 3000;
	npctalk "キャサリンゼータ : 結局、お爺さんの気持ちはきちんとキミに届いていなかったのですね……。残念です……。";
	sleep 3000;
	npctalk "キャサリンゼータ : 何にしろ、キミの魂がまた戻って来てもいいように、ここはしっかり管理しておいた方が良さそうです。";
	sleep 3000;
	npctalk "キャサリンゼータ : でも私はまだ、あきらめていません。真実もまだはっきりしていないし。だからまた良ければ、力を貸してください。";
	sleep 3000;
	npctalk "キャサリンゼータ : 外に出る出口は開けておきます。私が先に行くのでついてきてください。";
	sleep 3000;
	npctalk "キャサリンゼータ : ……いつかキミにも、お爺さんやみんなの気持ちをちゃんと届けられたらいいな。";
	sleep 3000;
	}
	npctalk "キャサリンゼータ : 非常口を開放しました。お礼も置いておきましたので、非常口から外に出て下さいね。";
	sleep 3000;
	disablenpc instance_npcname("キャサリンゼータ#6");
	enablenpc instance_npcname("#exwp1");
	end;
}

1@xm_d,218,150,5	script	#exwp1	PORTAL,{
	if (checkweight(2001000901,2,2001000910,1,6919,8) == 0) {
		mes "インベントリが一杯で報酬を受け取れません。";
		mes "インベントリを整理して下さい。";
		close;
	}
	mes "‐どうやら緊急用の脱出口が";
	mes "　何かの拍子で作動したようです。";
	mes "　この脱出口から";
	mes "　街に戻れるようですが";
	mes "　まだ工場に用事があるなら";
	mes "　出ない方が良いでしょう‐";
	next;
	if(select("まだ出ない","外に出る") == 1) {
		mes "‐あなたはその場を離れた‐";
		close;
	}
	close2;
	getitem 2001000901,2;
	getitem 2001000910,1;
	getitem 6919,8;
	warp "xmas",233,300;
	end;
}

1@xm_d,210,141,3	script	Packaged Present#1	4_TREASURE_BOX,{
	specialeffect EF_COIN;
	disablenpc instance_npcname( strnpcinfo(0) );
	initnpctimer;
	end;

OnTimer1000:
	switch( atoi(strnpcinfo(2)) ) {
	case 1:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(208,212), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 150)
			makeitem 644,1, 'xm_d_map$,209,141;// Gift_Box;
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,210,141;// Old_Violet_Box;
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,211,141;// Closedmind_Box
		break;
	case 2:
		.@num = rand(3,7);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(212,216), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 400)
			makeitem 603,1, 'xm_d_map$,213,141;// Old_Blue_Box
		if (rand(1,1000) > 700)
			makeitem 616,1, 'xm_d_map$,214,141;// Old_Card_Album
		if (rand(1,1000) > 950)
			makeitem 13442,1, 'xm_d_map$,215,141;// Old_Parasol
		break;
	case 3:
		.@num = rand(2,6);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(216,220), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 850)
			makeitem 7229,1, 'xm_d_map$,217,141;// Silver_Bullion
		if (rand(1,1000) > 800)
			makeitem 12246,1, 'xm_d_map$,218,141;// Magic_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2486,1, 'xm_d_map$,219,141;// Shadow_Walk_
		break;
	case 4:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(220,224), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 700)
			makeitem 7228,1, 'xm_d_map$,221,141;// Gold_Bullion
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,222,141;// Old_Violet_Box
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,223,141;// Closedmind_Box
		break;
	case 5:
		.@num = rand(3,7);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(224,228), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 150)
			makeitem 644,1, 'xm_d_map$,225,141;// Gift_Box
		if (rand(1,1000) > 700)
			makeitem 616,1, 'xm_d_map$,226,141;// Old_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2976,1, 'xm_d_map$,227,141;// Red_Lantern
		break;
	case 6:
		.@num = rand(2,6);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(208,212), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 400)
			makeitem 603,1, 'xm_d_map$,209,136;// Old_Blue_Box
		if (rand(1,1000) > 800)
			makeitem 12246,1, 'xm_d_map$,210,136;// Magic_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2977,1, 'xm_d_map$,211,136;// Hurt_Mind
		break;
	case 7:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(212,216), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 850)
			makeitem 7229,1, 'xm_d_map$,213,136;// Silver_Bullion
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,214,136;// Old_Violet_Box
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,215,136;// Closedmind_Box
		break;
	case 8:
		.@num = rand(3,7);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(216,220), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 700)
			makeitem 7228,1, 'xm_d_map$,217,136;// Gold_Bullion
		if (rand(1,1000) > 700)
			makeitem 616,1, 'xm_d_map$,218,136;// Old_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2978,1, 'xm_d_map$,219,136;// KindHeart
		break;
	case 9:
		.@num = rand(2,6);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(220,224), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 150)
			makeitem 644,1, 'xm_d_map$,221,136;// Gift_Box
		if (rand(1,1000) > 800)
			makeitem 12246,1, 'xm_d_map$,222,136;// Magic_Card_Album
		if (rand(1,1000) > 950)
			makeitem 18848,1, 'xm_d_map$,223,136;// Lush_Rose
		break;
	case 10:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(224,228), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 400)
			makeitem 603,1, 'xm_d_map$,225,136;// Old_Blue_Box
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,226,136;// Old_Violet_Box
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,227,136;// Closedmind_Box
		break;
	}
	stopnpctimer;
	end;
}
1@xm_d,214,141,3	duplicate(Packaged Present#1)	Packaged Present#2	4_TREASURE_BOX
1@xm_d,218,141,3	duplicate(Packaged Present#1)	Packaged Present#3	4_TREASURE_BOX
1@xm_d,222,141,3	duplicate(Packaged Present#1)	Packaged Present#4	4_TREASURE_BOX
1@xm_d,226,141,3	duplicate(Packaged Present#1)	Packaged Present#5	4_TREASURE_BOX
1@xm_d,210,136,3	duplicate(Packaged Present#1)	Packaged Present#6	4_TREASURE_BOX
1@xm_d,214,136,3	duplicate(Packaged Present#1)	Packaged Present#7	4_TREASURE_BOX
1@xm_d,218,136,3	duplicate(Packaged Present#1)	Packaged Present#8	4_TREASURE_BOX
1@xm_d,222,136,3	duplicate(Packaged Present#1)	Packaged Present#9	4_TREASURE_BOX
1@xm_d,226,136,3	duplicate(Packaged Present#1)	Packaged Present#10	4_TREASURE_BOX

// Warps
//==========================================
1@xm_d,79,129,0	warp2	#fac1wp2	2,2,1@xm_d,73,129
1@xm_d,184,109,0	warp2	#fac2wp2	2,2,1@xm_d,170,129
1@xm_d,130,178,0	warp2	#fac3wp	2,2,1@xm_d,130,193
1@xm_d,130,184,0	warp2	#fac3wp2	2,2,1@xm_d,129,173
1@xm_d,107,208,0	warp2	#fac4wp	2,2,1@xm_d,87,208
1@xm_d,95,208,0	warp2	#fac4wp2	2,2,1@xm_d,115,208
1@xm_d,152,208,0	warp2	#fac5wp	2,2,1@xm_d,167,208
1@xm_d,205,159,0	warp2	#fac6wp	2,2,1@xm_d,205,147

// Instance GM Functions :: mx_xm_ad
//==========================================
1@xm_d,1,1,3	script	#adsw	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "[Time Manager]";
		mes "What time would you like to return?";
		next;
		switch (select("Cancel:Factory No.1 Complete:Factory No.2 Complete:Factory No.3 Complete:Boss Start:Boss Complete")) {
		case 1:
			break;
		case 2:
			transform 1246,180000;
			enablenpc instance_npcname("#fac1bs");
			mapannounce 'xm_d_map$, "Coordinator's announcement: Huh? Where's everyone! How dare the workers be absent from their line and muck around!",bc_map,"0xff8800";
			warp 'xm_d_map$,70,125;
			break;
		case 3:
			for ( .@i = 1; .@i <= 10; ++.@i )
				disablenpc instance_npcname( "Worker#" + .@i );
			donpcevent instance_npcname("#fac2wpc") + "::OnStart";
			warp 'xm_d_map$,131,210;
			break;
		case 4:
			donpcevent instance_npcname("#fac3ct3") + "::OnEnd";
			warp 'xm_d_map$,131,210;
			break;
		case 5:
			donpcevent instance_npcname("#finalbs2") + "::OnStart";
			warp 'xm_d_map$,215,182;
			break;
		case 6:
			donpcevent instance_npcname("#finalbs2") + "::OnEnd";
			warp 'xm_d_map$,215,182;
			break;
		}
	}
	close;
}

1@xm_d,3,1,3	script	#adsw2	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		if (unitexists('Antonio) == true)
			getunitdata 'Antonio, .@mob;
		mapannounce 'xm_d_map$, "Factory announcement: Exist in " + .@mob[UMOB_X] + " - " + .@mob[UMOB_Y] + ".",bc_map,"0x00ff44";
	}
	end;

OnInstanceInit:
	'xm_d_map$ = instance_mapname("1@xm_d");

	// Warps
	disablenpc instance_npcname("#fac3wp");
	disablenpc instance_npcname("#fac3wp2");
	disablenpc instance_npcname("#fac4wp");
	disablenpc instance_npcname("#fac4wp2");
	disablenpc instance_npcname("#fac5wp");
	// disablenpc instance_npcname("#fac5wp2");
	disablenpc instance_npcname("#fac6wp");

	// Bgm
	disablenpc instance_npcname("#bgm01");
	disablenpc instance_npcname("#bgm02");
	disablenpc instance_npcname("#bgm03");
	disablenpc instance_npcname("#bgm04");
	disablenpc instance_npcname("#bgm05");
	disablenpc instance_npcname("#bgm06");

	// Step 1
	disablenpc instance_npcname("キャサリンゼータ#01");
	disablenpc instance_npcname("職員用の衣服ボックス#1");
	disablenpc instance_npcname("職員用の衣服ボックス#2");
	disablenpc instance_npcname("職員用の衣服ボックス#3");
	hideonnpc instance_npcname("#fac1ct");
	disablenpc instance_npcname("#fac1bs");
	disablenpc instance_npcname("#pck1");
	disablenpc instance_npcname("#alert1");

	// Step 2
	disablenpc instance_npcname("キャサリンゼータ#21");
	disablenpc instance_npcname("職員用の衣服ボックス#4");
	hideonnpc instance_npcname("#fac2ct");
	disablenpc instance_npcname("#fac2wpc");
	for ( .@i = 1; .@i < 11; ++.@i )
		disablenpc instance_npcname("Worker#" + .@i);

	// Step 3
	disablenpc instance_npcname("囚われたサンタ#3");
	hideonnpc instance_npcname("#fac3ct");
	hideonnpc instance_npcname("#fac3ct2");
	hideonnpc instance_npcname("#fac3ct3");

	// Boss
	disablenpc instance_npcname("#finalbs");
	hideonnpc instance_npcname("#finalbs2");
	disablenpc instance_npcname("セリーヌ・キミ#0");
	disablenpc instance_npcname("#jeton1");
	disablenpc instance_npcname("キャサリンゼータ#5");
	for ( .@i = 1; .@i < 10; ++.@i )
		hideonnpc instance_npcname("#eff_f0" + .@i);
	disablenpc instance_npcname("#bssk01");
	disablenpc instance_npcname("#bssk02");
	for ( .@i = 1; .@i < 5; ++.@i )
		hideonnpc instance_npcname("#crssk" + .@i);
	disablenpc instance_npcname("#kimion1");
	disablenpc instance_npcname("セリーヌ・キミ#2");
	disablenpc instance_npcname("#heal_c");

	// Exit
	disablenpc instance_npcname("#finalbs_e");
	disablenpc instance_npcname("#jeton2");
	disablenpc instance_npcname("キャサリンゼータ#6");
	disablenpc instance_npcname("#exwp1");
	for ( .@i = 1; .@i < 11; ++.@i )
		disablenpc instance_npcname("Packaged Present#" + .@i);
	end;
}
